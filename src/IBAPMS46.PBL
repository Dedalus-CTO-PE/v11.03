.***************************************************************************
.* System    :   EDWARD Patient Extract                                    *
.* Program   :   IBAPMS46                                                  *
.* Desc      :   Write EDWARD extract triggers for selected patients       * 
.*           :                                                             *
.***************************************************************************
.* Date      :   18 March 2022                                             *
.* Author    :   DA Sarkies                                                *
.* Function  :   User selects a date range, and an Visit Discharge type    * 
.*           :   and the program automatically updates all of the visit    *
.*           :   types within that date range. Used for EWD Extracts       *
.*           :                                                             *
.***************************************************************************
.* V11.02.01  25.07.2022 Ebon Clements           TSK 0921629               *
.*            Added trigger outpatient booking update option               *
.* 18.03.2022    DA Sarkies                         TSK 0915072            *
.*                  Created program                                        *
.***************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC       PATDSCFD/INC
          INC       PMSADWFD/INC
          INC       OUTBOAFD/INC
          INC       OUTSESFD/INC
          INC       OUTSITFD/INC
.
. LOCAL VARIABLE DEFINITION
. -------------------------
RECCOUNT  FORM      8
SITECODE  DIM       6                       * Site code
CURRDATE  DIM       8
DISPFDAT  DIM       11
DISPTDAT  DIM       11
STRTDATE  DIM       8
ENDDDATE  DIM       8
VISTDATE  DIM       11
ADPTTYPE  DIM	    3
.
.
PRGID     INIT      "IBAPMS46"
PRGNAM    INIT      "EDWARD Extract Trigger Generation       "
VERSION   INIT      "V11.03.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000         * proceed with clean up
.
ML1000    CALL      ENTP0000               * Select Admitted Patient Type
.
ML1100    CALL      DATE0000               * keyin date range
.
          CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML2000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      PROC0000               * process
.
ML9999    CHAIN     PGM                    * chain back to program
+
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*                             initialisation                               *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
.
          OPEN      PATDSCH2,"patdschf"
          OPEN      PMSADWA1,"pmsadwaf"
.
          DISPLAY   *P64:24,"outsitaf";
          OPEN      OUTSITA1,"outsitaf"
          MOVE      IDDD,SITECODE
          MOVE      IDDD,KEY6
          CALL      RDSITA1
.
          PACK      CFNAME WITH UID,FILSESA6        * OUTSESFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTSESA6,CFNAME
.
          PACK      CFNAME WITH UID,FILBOKA1        * OUTBOKFD
          DISPLAY   *P64:24,CFNAME;
          OPEN      OUTBOKA1,CFNAME
.
INIT9999  RETURN
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.*                                                                          *
.*    Returns:  EXIT    = FALSE (0)  Run clean up                           *
.*                        TRUE  (1)  Exit option selected                   *
.****************************************************************************
.
OPTN0000  DISPLAY   *P1:3,*EF,*P1:4,*V2LON,ZERO,*HOFF,". Exit":
                    *P1:5,*V2LON,ONE,*HOFF,". Run Report Only":
                    *P1:6,*V2LON,TWO,*HOFF,". Run Report and Trigger Records"
.
OPTN0500  KEYIN     *P1:8,*EL,"Select Option : ",*DV,UNDLN1:
                    *P17:8,*V2LON,COPTION
.
          COMPARE   ZERO,COPTION                 * exit selected ?
          GOTO      OPTN9500 IF EQUAL            * yes
.
          BRANCH    COPTION,OPTN9000,OPTN9000    * run clean-up
.
          BEEP
          GOTO      OPTN0500
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT
.
OPTN9999  RETURN
+
+
.***************************************************************************
.*                               ENTP0000                                  *
.*                          Enter Admitted Patient Type                    *
.***************************************************************************
.
ENTP0000  MOVE      SP70,ADPTTYPE
          KEYIN     *P1:10,*EF,"Enter Admitted Patient Type :",*DV,UNDLN1:
                    *P30:10,*V2LON,ADPTTYPE
.
          PACK      ADPTTYPE,ADPTTYPE,SP70
.
ENTP9999  RETURN

.***************************************************************************
.*                               DATE0000                                  *
.*                          accept date range                              *
.***************************************************************************
.
.---------- keyin first date range -------
.
DATE0000    MOVE       FALSE,EXIT
            DISPLAY    *P1:12,*EF,"Date From :":
                          *P1:13,"Date To   :";
.
            MOVE      "12",CVERT
            MOVE      "13",CCOL
            CALL      IBACLOCK
            PACK      CURRDATE,CCC,CYY,CMM,CDD
            REP       " 0",CURRDATE
            MOVE      CCC,CCENT
            MOVE      CYY,CYEAR
            MOVE      CMM,CMON
            MOVE      CDD,CDAY

            CALL      KEYDATE                          * enter the from date
            PACK      STRTDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",STRTDATE                    * from date
.
            MATCH     CURRDATE,STRTDATE
            IF        @EQUAL
              DISPLAY    *P1:24,*EL,*B,"Fix can't be run for the current date":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MOVE       "13",CVERT
            MOVE       "13",CCOL
            CALL      KEYDATE                          * enter the from date
            PACK      ENDDDATE,CCENT,CYEAR,CMON,CDAY   * move to form field
            REP       " 0",ENDDDATE                    * from date
.
            MATCH     CURRDATE,ENDDDATE
            IF        !@LESS 
              DISPLAY    *P1:24,*EL,*B,"Fix can't be after the current date":
                         " Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
              KEYIN      ANS;
              DISPLAY    *P1:24,*EL;
              GOTO       DATE0000
            ENDIF
.
            MATCH      STRTDATE,ENDDDATE
            GOTO       DATE9999 IF NOT LESS
.
            DISPLAY    *P1:24,*EL,*B,"2nd Range Must Be Greater Than 1st ":
                       "Range - Hit <",*V2LON,"ENTER",*HOFF,"> To Continue ";
            KEYIN      ANS;
            DISPLAY    *P1:24,*EL;
            GOTO       DATE0000
.
DATE9999    RETURN
+
.**************************************************************************
.*                               PROC0000              Called by: ML0000  *
.*                                                                        *
.**************************************************************************
.
PROC0000  CALL      HEAD0000               * Print Report Heading
.
          MOVE      ZERO,RECCOUNT          * Records Checked
.
          MATCH     "5  ",ADPTTYPE
          IF        @EQUAL
            CALL      OPAT0000             * Update OP visit trigger 5
            GOTO      PROC9000
          ENDIF
.
          MATCH     "14 ",ADPTTYPE
          IF        @EQUAL
            CALL      IPAT0000             * Update IP visit trigger 14
            GOTO      PROC9000
          ENDIF
.
PROC9000  CALL      UND80
          PRINT     *1," Number of records checked :",RECCOUNT
          DISPLAY   *P1:20,*EF,"Program Complete.";
          CALL      HITENTER
.
PROC9999  RETURN
+
.------------------------------------------------------------
. Process OP visit update trigger 5
.------------------------------------------------------------
OPAT0000  PACK      KEY25,SITECODE,STRTDATE,SP70
          CALL      RDSSESA6                * Sesssion for site and date
OPAT1000  CALL      RDKSESA6
          BRANCH    OVRCD,OPAT9999
.
          MATCH     OSESITE,OSTSITE         * Correct site
          GOTO      OPAT9999 IF NOT EQUAL
.
          MATCH     OSEDATE,ENDDDATE        * Session date
          GOTO      OPAT9999 IF LESS
.
          PACK      KEY28,OSESITE,OSECLIN,OSEDATE,OSESTRT,SP70
          CALL      RDSBOKA1
OPAT5000  CALL      RDKBOKA1                * Check all bookings
          BRANCH    OVRCD,OPAT1000
.
          MATCH     OBASITE,OSESITE         * Site
          GOTO      OPAT1000 IF NOT EQUAL
.
          MATCH     OBACLIN,OSECLIN         * Clinic
          GOTO      OPAT1000 IF NOT EQUAL
.
          MATCH     OBADATE,OSEDATE         * Date
          GOTO      OPAT1000 IF NOT EQUAL
.
          MATCH     OBASTRT,OSESTRT         * Start time
          GOTO      OPAT1000 IF NOT EQUAL
.
.  Booked, Attended and DNA
.
          IF        OBASTAT <> 1 & OBASTAT <> 4 & OBASTAT <> 5
            GOTO      OPAT5000
          ENDIF
.
          ADD       ONE,RECCOUNT           * Records Checked
          UNPACK    OBADATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VISTDATE
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          DISPLAY   *P25:15,*EL,*V2LON,"OP Visit: ",VISTDATE,SP4,OBAOUTNO
.
          CALL      WEDA0000                * Write EDWARD trigger
          PRINT     *1,"| ",OBAOUTNO,*12,"| ",VISTDATE,*80,"|"
.
          GOTO      OPAT5000
.
OPAT9999  RETURN
+
.------------------------------------------------------------
. Process IP visit update trigger 14
.------------------------------------------------------------
IPAT0000  PACK      KEY16,STRTDATE,SP70
          CALL      RDSDSCH2               * Loop discharge file using
IPAT1000  CALL      RDKDSCH2               * the discharge date index 2
          BRANCH    OVRCD,IPAT9999
.
          MATCH     DDATE,ENDDDATE         * Discharge Data after end date
          GOTO      IPAT9999 IF LESS

          ADD       ONE,RECCOUNT           * Records Checked
          UNPACK    DDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,VISTDATE
.
          COMPARE   "55",CLNO
          CALL      HEAD0000 IF NOT LESS
.
          DISPLAY   *P25:15,*EL,*V2LON,"IP Visit: ",VISTDATE,SP4,DADMNO
.
          CALL      WEDV0000                * Write EDWARD trigger
          PRINT     *1,"| ",DADMNO,*12,"| ",VISTDATE,*80,"|"
.
          GOTO      IPAT1000
.
IPAT9999  RETURN
+
.------------------------------------------------------------
. Print Report Heading
.------------------------------------------------------------
HEAD0000  CALL      IBACLOCK
          CALL      HEAD80
.
          UNPACK    STRTDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPFDAT
.
          UNPACK    ENDDDATE,CCENT,CYEAR,CMON,CDAY
          CALL      PACDATE
          MOVE      CPCDATE,DISPTDAT
.
          MATCH     "5  ",ADPTTYPE
          IF        @EQUAL
            PRINT     *20,"OP Booking Date :",DISPFDAT," to ",DISPTDAT
          ENDIF
.
          MATCH     "14 ",ADPTTYPE
          IF        @EQUAL
            PRINT     *20,"Discharge Date :",DISPFDAT," to ",DISPTDAT
          ENDIF
.
          ADD       ONE,CLNO
.
          IF        COPTION=1
            PRINT     *20,"Report Only"
          ELSE
            PRINT     *20,"Report and Trigger Records"
          ENDIF
.
          CALL      UND80
.
          MATCH     "5  ",ADPTTYPE
          IF        @EQUAL
            PRINT     *1,"| Visit    ",*12,"| Booking Date",*80,"|"
          ENDIF
.
          MATCH     "14 ",ADPTTYPE
          IF        @EQUAL
            PRINT     *1,"| Visit    ",*12,"| Discharge Date",*80,"|"
          ENDIF
.
          ADD       ONE,CLNO
.
          CALL      UND80
.
HEAD9999  RETURN
+
.-----------------------------------------------------------
. Write EDWARD update inpatient visit trigger type 14
.------------------------------------------------------------
WEDV0000  BRANCH    COPTION,WEDV9999        * Report Only
.
          MOVE      DADMNO,PMAWVISN
          MOVE      ADPTTYPE,PMAWTYPE
          PACK      PMAWOLDV,SP70,SP70,SP70,SP70
          PACK      PMAWNEWV,SP70,SP70,SP70,SP70
.
          CALL      IBACLOCK
          PACK      PMAWCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMAWCDAT
          MOVE      "00:00:00",PMAWCTIM
          MOVE      "IBAPMS46",PMAWCUID
          PACK      PMAWSPAR,SP70,SP70
.
          PACK      KEY27,PMAWCDAT,PMAWCTIM,PMAWVISN,PMAWTYPE,SP70
          CALL      RAPMADW1
          IF        OVRCD=1
            CALL      WRPMADW1
          ELSE
            CALL      UPPMADW1
          ENDIF
.
WEDV9999  RETURN
.
.-----------------------------------------------------------
. Write EDWARD update OP visit trigger type 5
.------------------------------------------------------------
WEDA0000  BRANCH    COPTION,WEDV9999        * Report Only
.
          MOVE      OBAOUTNO,PMAWVISN
          MOVE      ADPTTYPE,PMAWTYPE
          PACK      PMAWOLDV,SP70,SP70,SP70,SP70
          PACK      PMAWNEWV,SP70,SP70,SP70,SP70
.
          CALL      IBACLOCK
          PACK      PMAWCDAT,CCC,CYY,CMM,CDD
          REP       " 0",PMAWCDAT
          MOVE      "00:00:00",PMAWCTIM
          MOVE      "IBAPMS46",PMAWCUID
          PACK      PMAWSPAR,SP70,SP70
.
          PACK      KEY27,PMAWCDAT,PMAWCTIM,PMAWVISN,PMAWTYPE,SP70
          CALL      RAPMADW1
          IF        OVRCD=1
            CALL      WRPMADW1
          ELSE
            CALL      UPPMADW1
          ENDIF
.
WEDA9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
.
          INC       STD001IO
.
          INC       PATDSCIO/INC
          INC       PMSADWIO/INC
          INC       OUTBOAIO/INC
          INC       OUTSESIO/INC
          INC       OUTSITIO/INC
