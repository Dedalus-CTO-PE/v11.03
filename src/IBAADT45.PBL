.***************************************************************************
.* Program   :   IBAADT45                                                  *
.* Desc      :   Casemix Code Matrix Report Program                        *
.***************************************************************************
.* Date      :   31/10/2000                                                *
.* Author    :   Ebon Clements                                             *
.* Funtion   :   Print details of the casemix funding matrix               * 
.* Mods      :                                                             *
.*                                                                         *
.* V11.01.03 06/10/2021 J.Tan            TSK 0901515                       *
.*           Added for Primary ICD Procedures/Operation                    *
.* V11.01.02 07/04/2021  Tracey Nguyen   TSK 0901515                       *
.*           Recompiled for PMSCMTFD - Added new fields                    *
.*           a) 1 File type for ICD procedure (PMCCIPF)                    *
.*           b) 3 new CMBS Field Type (PMCCFTS4/PMCCFT5C/PMCCFT6C)         *
.*           c) 3 new CMBS Codes From (PMCCPC4F,PMCCSC5F,PMCCTC6F)         *
.*           d) 3 new CMBS Codes To   (PMCCPC4T/PMCCSC5T,PMCCTC6T)         *
.* V11.01.01 10/03/2021  Tracey Nguyen   TSK 0901515                       *
.*           Recompiled for PMSCMTFD - Added Primary ICD Procedure 1-5     *
.*           (PMCCICP1-5)                                                  *
.*-------------------------------------------------------------------------*
.* V11.00.01  12/03/2020  J.Tan          TSK 0838262                       *
.*            Removed PATITMKY (not used)                                  *
.* V10.04.01  15/06/2014 Jill Parkinson CAR 301639                         *
.*            Moved call TFILENAM to INIT0000                              *
.* V10.03.01  19/12/2012 Patrick Adair                          CAR 261630 *
.*                       Removed port number from temp file name           *
.*        V10.01.01 24/01/2011  Ebon Clements  CAR 233051                  *
.*                  Removed old/new file, added contract and min days stay *
.*        V9.09.02  14/03/2008  Jill Habasque  CAR 163990                  *
.*                  Increased length of CMDLINE                            *
.*        V9.09.01  24/01/2008  Jill Habasque  CAR 159943                  *
.*                  Fixed tempfile to be the same as the FD                *
.*        V9.08.01  08/05/2007  Mike Laming  CAR 137125 HDP 2007 DRG 5.2   *
.*                  Mods to PATDGWDS - Added PTDWDRGV to PATDGWFD          *
.***************************************************************************
.
.    INCLUDES
. ------------------
.
          INC       STD001FD
          INC       STDHLPDF
          INC       STDKWSDF
          INC       IBASEQFD/INC                 * Sequential Numbers File
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATCMCFD/INC
          INC       PMSCIDFD/INC
          INC       PATDGWFD/INC
          INC       PATFN1FD/INC
          INC       PATITMFD/INC
          INC       PMSCMTFD/INC
          INC       PATMI1FD/INC
          INC       PATKCOFD/INC      * ICD10 Operations Key Words
          INC       PATOICFD/INC      * ICD9 Operations key Words
          INC       PATKCDFD/INC      * ICD10 Diagnosis Key Words
          INC       PATDICFD/INC      * ICD9 Diagnosis Key Words
          INC       PATICDFD/INC      * ICD9 Diagnosis Key Words
          INC       PATICOFD/INC      * ICD9 Diagnosis Key Words
          INC       TFILEVAR/INC                 * Generate Temp File Name
          INC       WEBERRFD/INC                 * Web Server Error Logging
.
.    LOCAL VARIABLES
. ------------------
.
CODE      DIM       3
CONTDESC  DIM       34
CURRDATE  DIM       8
CMDLINE   DIM       127
CMAUDESC  DIM       2
DRGCODE   DIM       4
DRGVERZ   DIM       3                                                 *I-137125
DV1       DIM       1                                                 *I-137125
DV2       DIM       1                                                 *I-137125
DESCR1    DIM       20
DESCR2    DIM       20
ENDCMCD   DIM       9
ENDCONT   DIM       6
ENDHFTY   DIM       3
ENDCCOD   DIM       3
ENDHFUN   DIM       6
ERASE     INIT      "iserase "
FRCONT    DIM       6
FRHFUN    DIM       5
FRHFTY    DIM       5
FRCMCD    DIM       5
FUNDDESC  DIM       34
FRCCOD    DIM       5
ISBUILD   INIT      "isbuild "
NEWDITEM  FORM      2
PRTFLG    FORM      1
RANGE     FORM      1
SAVKEY9   DIM       9
SAVKEY24  DIM       24
SP40      INIT      "                                        "
STRCONT   DIM       6
STRHFTY   DIM       3
STRCCOD   DIM       3
STRHFUN   DIM       6
STRCMCD   DIM       9
SEPPGE    FORM      1
TOCCOD    DIM       5
TOCONT    DIM       6
TOHFUN    DIM       5
TOHFTY    DIM       5
TOCMCD    DIM       5
.
TEMPFILE  DIM       8
UKEY      INIT      " 353 U1-3,260-265,4-9,10-12,259-259,13-21,22-27 U300-305"
.
.         Temporary File Definition
.         -------------------------
.
TEMP1  IFILE SQL, FIXED=353, KEY="U1-3,260-265,4-9,10-12,259-259,13-21,22-27"
.
.NAME  TYPE  LENGTH  PHYSICAL  START   DESCRIPTION
.----  ----  ------  --------  -----   -----------
XCCOD  DIM       3      3        1     Claim code
XHFUN  DIM       6      6        4     Health Fund
XHFTY  DIM       3      3        10    Health Fund Type
XCMCD  DIM       9      9        13    Casemix Code
XCCNT  DIM       6      6        22    Counter
XPODY  FORM      6      4        28    Pre Op Cut Off Days
XNPDY  FORM      6      4        32    No Pay Days
XHBPT  FORM      6      4        36    High Boundary Point/Max Days Stay
XCCDY  FORM      6      4        40    Critical Care Cut Off
XFTDG  FORM      1      2        44    Field type for Disch DRG
.                                         Field type=0 - Ignore
.                                                    1 - Mandatory
.                                                    2 - Must be blank
.                                                    3 - Exclusive
XDDRG  DIM       4      4        46    Discharge DRG
XFTCM  FORM      1      2        50    Field type for CMBS
XPCMF  DIM       9      9        52    Primary CMBS from
XPCMT  DIM       9      9        61    Primary CMBS to
XFTSC  FORM      1      2        70    Field type for Secondary CMBS
XSCMF  DIM       9      9        72    Secondary CMBS fron
XSCMT  DIM       9      9        81    Secondary CMBS to
XFTTC  FORM      1      2        90    Field type for Tertiary CMBS from
XTCMF  DIM       9      9        92    Tertiary CMBS from
XTCMT  DIM       9      9        101   Tertiary CMBS to
XFTDS  FORM      1      2        110   Field type for disch status
XDSTA  DIM       3      3        112   Discharge Status 1
XDST2  DIM       3      3        115   Discharge Status 2                 
XDST3  DIM       3      3        118   Discharge Status 3              
XDST4  DIM       3      3        121   Discharge Status 4           
XDST5  DIM       3      3        124   Discharge Status 5        
XFTDD  FORM      1      2        127   Field type for disch destination
XDDES  DIM       3      3        129   Discharge Destination 1
XDDE2  DIM       3      3        132   Discharge Destination 2
XDDE3  DIM       3      3        135   Discharge Destination 3
XDDE4  DIM       3      3        138   Discharge Destination 4
XDDE5  DIM       3      3        141   Discharge Destination 5
XFTAS  FORM      1      2        144   Field type for Adm Source
XASRC  DIM       3      3        146   Admission Source 1
XASR2  DIM       3      3        149   Admission Source 2
XASR3  DIM       3      3        152   Admission Source 3
XASR4  DIM       3      3        155   Admission Source 4
XASR5  DIM       3      3        158   Admission Source 5
XFT1C  FORM      1      2        161   Field type for ICD disease
XPICD  DIM       9      9        163   Primary ICD disease 1
XPIC2  DIM       9      9        172   Primary ICD disease 2
XPIC3  DIM       9      9        181   Primary ICD disease 3
XPIC4  DIM       9      9        190   Primary ICD disease 4
XPIC5  DIM       9      9        199   Primary ICD disease 5
XFTAT  FORM      1      2        208   Field type for Adm type
XATYP  DIM       3      3        210   Admission Type 1
XATY2  DIM       3      3        213   Admission Type 2
XATY3  DIM       3      3        216   Admission Type 3
XATY4  DIM       3      3        219   Admission Type 4
XATY5  DIM       3      3        222   Admission Type 5
XFTAU  FORM      1      2        225   Field type for Adm UDF
XAUDF  DIM       3      3        227   Admission UDF 1
XAUD2  DIM       3      3        230   Admission UDF 2
XAUD3  DIM       3      3        233   Admission UDF 3
XAUD4  DIM       3      3        236   Admission UDF 4
XAUD5  DIM       3      3        239   Admission UDF 5
XFTDU  FORM      1      2        242   Field type for Dis UDF
XDUDF  DIM       3      3        244   Discharge UDF 1
XDUD2  DIM       3      3        247   Discharge UDF 2
XDUD3  DIM       3      3        250   Discharge UDF 3
XDUD4  DIM       3      3        253   Discharge UDF 4
XDUD5  DIM       3      3        256   Discharge UDF 5
XTYPE  DIM       1      1        259   Record Type
.                                         O - Overnight
.                                         D - Daycase
.                                         E - Either
XCONT  DIM       6      6        260   Contract ID
XMIND  FORM      6      4        266   Minimum Days Stay
XSPAR  DIM       30     30       270   Spare
XITEM  DIM       6      6        300   Item number on screen
.
XFTPF  FORM      1      2        306   Field type for ICD Procedure
XPIP1  DIM       9      9        308   Primary ICD Procedure 1
XPIP2  DIM       9      9        317   Primary ICD Procedure 2
XPIP3  DIM       9      9        326   Primary ICD Procedure 3
XPIP4  DIM       9      9        335   Primary ICD Procedure 4
XPIP5  DIM       9      9        344   Primary ICD Procedure 5
.
.End of Record                   353    
.
XFITEM FORM      6
XCOUNT FORM      6
.
PRGID     INIT      "IBAADT45"
PRGNAM    INIT      "Casemix Code Matrix Report"
VERSION   INIT      "V11.03.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
ML0000    CALL      INIT0000                * Initialisation and open files
.
ML1000    CALL      SELO0000                * Select option for file
          BRANCH    EXIT,ML9000
.
ML2000    CALL      KILL0000                * Delete temp file
          CALL      CREA0000                * Create temp file 
          CALL      KEYN0000                * Keyin contract id range    
          CALL      KEYC0000                * Keyin claim code range    
          CALL      KEYH0000                * Keyin health fund range
          CALL      KEYT0000                * Keyin fund table range
          CALL      KEYM0000                * Keyin casemix code range
          CALL      KEYS0000                * Page break on each matrix
.
ML2500    CALL      CONTQST                 * OK to continue ?
          BRANCH    CEXIT,ML3000,ML2000,ML0000
          BEEP
          GOTO      ML2500
.
ML3000    CALL      UPTM0000                * Load data into temp file
          CALL      PRNT0000                * Print details in temp file
          GOTO      ML0000                  
.
ML9000    CALL      KILL0000                * Remove temp file
.
ML9999    CHAIN     PGM

+
.**************************************************************************
.                               INIT0000                                  *
.  The initialisation routine is used to display headings and open files  *
.**************************************************************************
INIT0000  CALL      DISPHEAD
.
          DISPLAY   *P56:24,"Opening pmscmtaf";
          OPEN      PMSCMTA1,"pmscmtaf"
.
          DISPLAY   *P64:24,"pati10of";
          OPEN      PATI10O1,"pati10of"
.
          DISPLAY   *P64:24,"patcodes";
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P64:24,"patcmcaf";
          OPEN      PATCMCA1,"patcmcaf"
.
          DISPLAY   *P64:24,"pmscidaf";
          OPEN      PMSCIDA1,"pmscidaf"
.
          DISPLAY   *P64:24,"patdgwaf";
          OPEN      PATDGWA1,"patdgwaf"
.
          DISPLAY   *P64:24,"patitemn";
          OPEN      PATITEM1,"patitemn"
.
          DISPLAY   *P64:24,"paticdof";
          OPEN      PATICDO1,"paticdof"
.
          DISPLAY   *P64:24,"pati10df";
          OPEN      PATI10D1,"pati10df"
.
          DISPLAY   *P64:24,"patfn1af";
          OPEN      PATFN1A1,"patfn1af"
. 
          DISPLAY   *P64:24,"pmscmtaf";
          OPEN      PMSCMTA1,"pmscmtaf"
.
          CALL      TFILENAM                     * Generate temporary file name
          MOVE      TFILNAME,TEMPFILE
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,NINTY5;*147,PTCNCMAU,PTCNCMDU
.
INIT9999  RETURN
+
.**************************************************************************
.                               SELO0000                                  *
.  Select report to run                                                   *
.**************************************************************************
SELO0000  MOVE      ZERO,EXIT
          CALL      DISPHEAD
          DISPLAY   *P1:4,*EF,*V2LON,"0",*HOFF," = Exit":
                    *P1:5,*V2LON,"1",*HOFF," = Casemix Sequence";
.
          KEYIN     *P1:7,"Select Option : ",*HOFF,OPTION
          BRANCH    OPTION,SELO9000
          COMPARE   ZERO,OPTION
          GOTO      SELO9500 IF EQUAL
.
          BEEP
          GOTO      SELO0000
.
SELO9000  MOVE      ZERO,EXIT
          GOTO      SELO9999
.
SELO9500  MOVE      ONE,EXIT
.
SELO9999  RETURN
+
.**************************************************************************
.                               KEYN0000                                  *
.  Keyin selection details for contract Id                                *
.**************************************************************************
KEYN0000  UNPACK    SP70,FRCONT,TOCONT,STRCONT,ENDCONT

          DISPLAY   *P1:8,*EF:
                    *P5:8,"Contract Id From : ":
                    *P5:9,"Contract Id To   : ";
.
. ------  keyin the contract Id
.
          KEYIN     *P24:8,FRCONT
KEYN1500  PACK      FRCONT,FRCONT,SP70
          MOVE      FRCONT,STRCONT
.
          MATCH     SP70,FRCONT
          GOTO      KEYN1000 IF EQUAL
.
          PACK      KEY6,FRCONT,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,KEYN9000
.
          DISPLAY   *P31:8,PMCIDESC
          GOTO      KEYN5000
.
KEYN1000  DISPLAY   *P31:8,*V2LON,"Start",*HOFF;
          MOVE      "Start",FRCONT
          MOVE      ZERO,EXIT
.
KEYN5000  KEYIN     *P24:9,TOCONT
          PACK      TOCONT,TOCONT,SP70
          MOVE      TOCONT,ENDCONT
.
          MATCH     SP70,TOCONT
          GOTO      KEYN6000 IF EQUAL
.
          PACK      KEY6,TOCONT,SP70
          CALL      RDPMCID1
          BRANCH    OVRCD,KEYN9000
.
          DISPLAY   *P31:9,PMCIDESC
          GOTO      KEYN7000
.
KEYN6000  DISPLAY   *P31:9,*V2LON,"End",*HOFF;
          MOVE      "End  ",TOCONT
          MOVE      ZERO,EXIT
          GOTO      KEYN9999
.
KEYN7000  PACK      STRCONT,STRCONT,SP10
          PACK      ENDCONT,ENDCONT,SP10
          MATCH     STRCONT,ENDCONT
          GOTO      KEYN9999 IF NOT LESS
.
          DISPLAY   *P1:24,"To contract must be greater than from contract. ";
          CALL      HITENTER
          GOTO      KEYN5000
.
KEYN9000  MOVE      ONE,EXIT
.
KEYN9999  RETURN

+
.**************************************************************************
.                               KEYC0000                                  *
.  Keyin selection details for claim code                                 *
.**************************************************************************
KEYC0000  UNPACK    SP70,FRCCOD,TOCCOD,STRCCOD,ENDCCOD

          DISPLAY   *P1:8,*EF:
                    *P5:8,"Claim Code From       : ":
                    *P5:9,"Claim Code To         : ";
.
. ------  keyin claim code from ------
.
          MOVE      TWENTY9,ECOL
          MOVE      EIGHT,EVERT
          MOVE      "CL",CODE
          CALL      PATCODKY
          BRANCH    EXIT,KEYC1000,KEYC9000
          DISPLAY   *P38:EVERT,TDESC
          MOVE      ACODE,STRCCOD
          MOVE      ACODE,FRCCOD
          GOTO      KEYC5000
.
KEYC1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          DISPLAY   *P38:EVERT,SP40
          MOVE      "Start",FRCCOD
          MOVE      ZERO,EXIT  
.
. ------  keyin claim code to------
.
KEYC5000  MOVE      TWENTY9,ECOL
          MOVE      NINE,EVERT
          MOVE      "CL",CODE
          CALL      PATCODKY
          BRANCH    EXIT,KEYC6000,KEYC9000
          DISPLAY   *P38:EVERT,TDESC
          MOVE      ACODE,ENDCCOD
          MOVE      ACODE,TOCCOD
          GOTO      KEYC7000
.
KEYC6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          DISPLAY   *P38:EVERT,SP40
          MOVE      "End  ",TOCCOD
          MOVE      ZERO,EXIT
          GOTO      KEYC9999
.
KEYC7000  PACK      STRCCOD,STRCCOD,SP10 
          PACK      ENDCCOD,ENDCCOD,SP10
          MATCH     STRCCOD,ENDCCOD
          GOTO      KEYC9999 IF NOT LESS
          DISPLAY   *P1:24,"To code must be greater than from code. ";
          CALL      HITENTER
          GOTO      KEYC5000
.
KEYC9000  MOVE      ONE,EXIT
.
KEYC9999  RETURN
+
.**************************************************************************
.                               KEYH0000                                  *
.  Keyin selection details for Health Fund                                *
.**************************************************************************
KEYH0000  UNPACK    SP70,FRHFUN,TOHFUN,STRHFUN,ENDHFUN

          DISPLAY   *P1:11,*EF:
                    *P5:11,"Health Fund From      : ":
                    *P5:12,"Health Fund To        : ";
. 
. ------  keyin the health fund from ------
.
          MOVE      TEN1,EVERT
          CALL      PATFNDKY
          MOVE      HCODE,AFUNDH
          BRANCH    EXIT,KEYH1000,KEYH9000
          DISPLAY   *P38:EVERT,HFNAME
          MOVE      AFUNDH,FRHFUN
          MOVE      AFUNDH,STRHFUN
          GOTO      KEYH5000
.
KEYH1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          DISPLAY   *P38:EVERT,SP40
          MOVE      "Start",FRHFUN
          MOVE      ZERO,EXIT
. 
. ------  keyin the health fund from ------
.
KEYH5000  MOVE      TEN2,EVERT
          CALL      PATFNDKY
          MOVE      HCODE,AFUNDH
          BRANCH    EXIT,KEYH6000,KEYH9000
          DISPLAY   *P38:EVERT,HFNAME
          MOVE      AFUNDH,TOHFUN
          MOVE      AFUNDH,ENDHFUN
          GOTO      KEYH7000
.
KEYH6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          DISPLAY   *P38:EVERT,SP40
          MOVE      "End  ",TOHFUN
          MOVE      ZERO,EXIT
          GOTO      KEYH9999
.
KEYH7000  PACK      STRHFUN,STRHFUN,SP10
          PACK      ENDHFUN,ENDHFUN,SP10
          MATCH     STRHFUN,ENDHFUN
          GOTO      KEYH9999 IF NOT LESS
          DISPLAY   *P1:24,"To fund must be greater than from fund. ";
          CALL      HITENTER
          GOTO      KEYH5000
.
KEYH9000  MOVE      ONE,EXIT
.
KEYH9999  RETURN
+
.**************************************************************************
.                               KEYT0000                                  *
.  Keyin selection details for Health Fund Type                           *
.**************************************************************************
KEYT0000  UNPACK    SP70,FRHFTY,TOHFTY,STRHFTY,ENDHFTY

          DISPLAY   *P1:14,*EF:
                    *P5:14,"Health Fund Table Type From : ":
                    *P5:15,"Health Fund Table Type To   : ";
.
. ------  keyin the health fund table type ------
.
          MOVE      "HT",CODE
          MOVE      TEN4,EVERT
          MOVE      THIRTY5,ECOL
          CALL      PATCODKY
          BRANCH    EXIT,KEYT1000,KEYT9000
          DISPLAY   *P41:EVERT,TDESC
          MOVE      ACODE,FRHFTY
          MOVE      ACODE,STRHFTY
          GOTO      KEYT5000
.
KEYT1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          DISPLAY   *P41:EVERT,SP40
          MOVE      "Start",FRHFTY 
          MOVE      ZERO,EXIT
.
KEYT5000  MOVE      "HT",CODE
          MOVE      TEN5,EVERT
          CALL      PATCODKY
          BRANCH    EXIT,KEYT6000,KEYT9000
          DISPLAY   *P41:EVERT,TDESC
          MOVE      ACODE,TOHFTY
          MOVE      ACODE,ENDHFTY
          GOTO      KEYT7000
.
KEYT6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          DISPLAY   *P41:EVERT,SP40
          MOVE      "End  ",TOHFTY 
          MOVE      ZERO,EXIT
          GOTO      KEYT9999
.
KEYT7000  PACK      STRHFTY,STRHFTY,SP10
          PACK      ENDHFTY,ENDHFTY,SP10 
          MATCH     STRHFTY,ENDHFTY 
          GOTO      KEYT9999 IF NOT LESS
          DISPLAY   *P1:24,"To table must be greater than from table. ";
          CALL      HITENTER
          GOTO      KEYT5000
.
KEYT9000  MOVE      ONE,EXIT
.
KEYT9999  RETURN
+
.**************************************************************************
.                               KEYM0000                                  *
.  Keyin selection details for Casemix code                               *
.**************************************************************************
KEYM0000  UNPACK    SP70,FRCMCD,TOCMCD,STRCMCD,ENDCMCD

          DISPLAY   *P1:17,*EF:
                    *P5:17,"Casemix Code From : ":
                    *P5:18,"Casemix Code To   : ";
.
          MOVE      TEN7,CVERT
          MOVE      TEN7,EVERT
          MOVE      TWENTY5,CCOL
          MOVE      TWENTY5,ECOL
          CALL      KPTCAA00
          BRANCH    EXIT,KEYM1000,KEYM9000
          DISPLAY   *P38:CVERT,PTCADES1
          MOVE      PTCACODE,STRCMCD 
          MOVE      PTCACODE,FRCMCD 
          GOTO      KEYM5000
.    
KEYM1000  DISPLAY   *PECOL:EVERT,*V2LON,"Start",*HOFF;
          DISPLAY   *P32:EVERT,SP40
          MOVE      "Start",FRCMCD
          MOVE      ZERO,EXIT
.
KEYM5000  MOVE      TEN8,CVERT
          MOVE      TEN8,EVERT
          MOVE      TWENTY5,CCOL
          MOVE      TWENTY5,ECOL
          CALL      KPTCAA00
          BRANCH    EXIT,KEYM6000,KEYM9000
          DISPLAY   *P38:CVERT,PTCADES1
          MOVE      PTCACODE,ENDCMCD 
          MOVE      PTCACODE,TOCMCD 
          GOTO      KEYM7000
.
KEYM6000  DISPLAY   *PECOL:EVERT,*V2LON,"End",*HOFF;
          DISPLAY   *P32:EVERT,SP40
          MOVE      "End  ",TOCMCD
          MOVE      ZERO,EXIT
          GOTO      KEYM9999
.
KEYM7000  PACK      STRCMCD,STRCMCD,SP10
          PACK      ENDCMCD,ENDCMCD,SP10   
          MATCH     STRCMCD,ENDCMCD
          GOTO      KEYM9999 IF NOT LESS
          DISPLAY   *P1:24,"To table must be greater than from table. ";
          CALL      HITENTER
          GOTO      KEYM5000
.
KEYM9000  MOVE      ONE,EXIT
.
KEYM9999  RETURN
+
.****************************************************************************
.*                              KEYS0000               Called by:           *
.*               Separate page per casemix matrix                           *
.****************************************************************************
KEYS0000  KEYIN     *P5:20,*EL:
                    "Do you want each Casemix Matrix on a separate page (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") ? : ",*V2LON,ANS;
          REP       UPPLOW,ANS
          DISPLAY   *P66:20,*EL,*V2LON,ANS;
.
          MATCH     "Y",ANS
          GOTO      KEYS8000 IF EQUAL
          MATCH     "N",ANS
          GOTO      KEYS9000 IF EQUAL
          BEEP
          GOTO      KEYS0000
.
KEYS8000  MOVE      ONE,SEPPGE
          GOTO      KEYS9999
.
KEYS9000  MOVE      ZERO,SEPPGE
.
KEYS9999  RETURN
+
.****************************************************************************
.*                              KILL0000               Called by:           *
.*               close and erase the temporary file                         *
.****************************************************************************
KILL0000  CLOSE     TEMP1                        * close file
          CLEAR     CMDLINE
          PACK      CMDLINE,ERASE,TEMPFILE       * set file erase command
          EXECUTE   CMDLINE,TASKID               * erase temp file
.
KILL9999 RETURN
+
.**************************************************************************
.*                              CREA0000               Called by:         *
.*             create a new temporary file                                *
.**************************************************************************
.
CREA0000  CALL      KILL0000                     * remove existing file
.
          CLEAR     CMDLINE
          PACK      CMDLINE,ISBUILD,TEMPFILE,UKEY
          EXECUTE   CMDLINE,TASKID               * create temporary index file
.
          OPEN      TEMP1,TEMPFILE               * open temp index file
.
CREA9999  RETURN
+
.**************************************************************************
.*                               UPTM0000              Called by:         *
.*        Write details of matrix to temp1                                *
.**************************************************************************
UPTM0000  PACK      KEY34,STRCCOD,SP40
          CALL      RSPMCMT1
UPTM1000  CALL      RKPMCMT1
          BRANCH    OVRCD,UPTM9999
.
          MATCH   SP3,ENDCCOD                  * Claim code to
          IF        !@EQUAL
            MATCH     PMCCCCOD,ENDCCOD
            GOTO      UPTM1000 IF LESS
          ENDIF
.
          MATCH   SP6,STRCONT                  * Contract Id from
          IF        !@EQUAL
            MATCH    STRCONT,PMCCCONT
            GOTO     UPTM1000 IF LESS
          ENDIF
.
          MATCH   SP6,ENDCONT                  * Contract Id to
          IF        !@EQUAL
            MATCH     PMCCCONT,ENDCONT
            GOTO      UPTM1000 IF LESS
          ENDIF
.
          MATCH   SP6,STRHFUN                  * Health fund from
          IF        !@EQUAL
            MATCH    STRHFUN,PMCCHFUN
            GOTO     UPTM1000 IF LESS
          ENDIF
.
          MATCH   SP6,ENDHFUN                  * Health fund to 
          IF        !@EQUAL
            MATCH     PMCCHFUN,ENDHFUN
            GOTO      UPTM1000 IF LESS
          ENDIF
.
          MATCH   SP3,STRHFTY                  * Fund table from  
          IF        !@EQUAL
            MATCH     STRHFTY,PMCCHFTY
            GOTO      UPTM1000 IF LESS
          ENDIF
.
          MATCH   SP3,ENDHFTY                  * Fund tabel to
          IF        !@EQUAL
            MATCH     PMCCHFTY,ENDHFTY
            GOTO      UPTM1000 IF LESS
          ENDIF
.
          MATCH   SP9,STRCMCD                  * Casemix code from
          IF        !@EQUAL
            MATCH     STRCMCD,PMCCCMCD
            GOTO      UPTM1000 IF LESS
          ENDIF
.
          MATCH   SP9,ENDCMCD                 * Casemix code to 
          IF        !@EQUAL
            MATCH     PMCCCMCD,ENDCMCD
            GOTO      UPTM1000 IF LESS
          ENDIF
.
. ------  write to tempfile ------
.
          CALL      MOVE0000
          PACK      KEY34,PMCCCCOD,PMCCCONT,PMCCHFUN,PMCCHFTY,PMCCTYPE:
                          PMCCCMCD,DPMCCCNT
          CALL      WRTEMP1
          ADD       ONE,XFITEM
          GOTO      UPTM1000
.
UPTM9999  RETURN
+
.**************************************************************************
.*                               PRNT0000              Called by:         *
.*        Print details of temp file                                      *
.**************************************************************************
PRNT0000  CALL      IBACLOCK
          CALL      HEAD132
          CALL      UND132
          MOVE      ZERO,PRTFLG
          PRINT     *35,"Contract Id From  : ",FRCONT:
                        " To : ",TOCONT;
          PRINT     *35,"Claim Code From   : ",FRCCOD:
                        "  To : ",TOCCOD;
          PRINT     *N,*35,"Health Fund From  : ",FRHFUN:
                        "  To : ",TOHFUN;
          PRINT     *N,*35,"Fund Type From    : ",FRHFTY:
                        "  To : ",TOHFTY ;
          PRINT     *N,*35,"Casemix Code From : ",FRCMCD:
                        "  To : ",TOCMCD,*N;
          CALL      UND132
          PACK      KEY34,SP30
          CALL      RSTEMP1
PRNT1000  CALL      RKTEMP1 
          BRANCH    OVRCD,PRNT9000
          MOVE      XCCNT,XCOUNT
          COMPARE   ZERO,XCOUNT
          IF        @EQUAL
.
            IF        SEPPGE = 1
              COMPARE   ZERO,PRTFLG
              GOTO      PRNT1500 IF EQUAL
              CALL      HEAD132
              CALL      UND132
            ENDIF
.
PRNT1500    PACK      DESCR1,SP70
            PACK      DESCR2,SP70
            PACK      HFNAME,SP70
            PACK      PTCADES1,SP70
            PACK      FUNDDESC,SP70
            PACK      PMCIDESC,SP70
.
            MOVE      "CL",CODE
            PACK      KEY5,CODE,XCCOD,SP10
            CALL      RDCODE1 
            IF        OVRCD = 0
              MOVE      TDESC,DESCR1
            ENDIF
.
            MOVE      "HT",CODE
            PACK      KEY5,CODE,XHFTY,SP10
            CALL      RDCODE1
            IF        OVRCD = 0
              MOVE      TDESC,DESCR2
            ENDIF
.
            PACK      KEY14,XHFUN,ZERO,ZERO,ZERO,ZERO,SP4
            CALL      RDFUND1
            MOVE      HFNAME,FUNDDESC
.
            PACK      KEY9,XCMCD 
            CALL      RDPTCMC1
.
            PACK      KEY6,XCONT,SP70
            CALL      RDPMCID1
            MOVE      PMCIDESC,CONTDESC
.
            PRINT     *N,*1,"Contract Id       : ",XCONT,*35,CONTDESC:
                      *70,"Pre Op Cut Off Days   : ",XPODY:
.
                      *N,*1,"Claim Code        : ",XCCOD,*35,DESCR1:
                      *70,"No Pay Days           : ",XNPDY:
.
                      *N,*1,"Health Fund       : ",XHFUN,*35,FUNDDESC:
                      *70,"Min Days Stay         : ",XMIND:
.
                      *N,*1,"Health Fund Table : ",XHFTY,*35,DESCR2:
                      *70,"Max Days Stay         : ",XHBPT:
.
                      *N,*1,"Casemix Code      : ",XCMCD,*35,PTCADES1:
                      *70,"Critical Care Cut Off : ",XCCDY,*N
.
            CALL      UND132
            PRINT      *1,"|Dischg|   Primary CMBS       |":
                          "   Secondary CMBS     |":
                          "   Tertiary CMBS      |Disch |Disch |Admiss|":
                          "PrimaryICD10|Admiss|U/Def |U/Def |";
.       
            PRINT   *N,*1,"|Drg   |   From      To       |":
                          "   From      To       |":
                          "   From      To       |Status|Destin|Source|":
                          "Diag./Oper. |Type  |Admiss|Disch |",*N;
.
            CALL       UND132
            MOVE       ONE,PRTFLG
            GOTO       PRNT1000 
          ENDIF
          PRINT      "(",XFTDG,")",XDDRG,"|",*9,"(",XFTCM,")",*12,XPCMF:
                     *22,XPCMT,*31,"|",*32,"(",XFTSC,")",*35,XSCMF,*45,XSCMT: 
                     *54,"|",*55,"(",XFTTC,")",*58,XTCMF,*68,XTCMT,*77,"|":
                     *78,"(",XFTDS,")",XDSTA,*84,"|",*85,"(",XFTDD,")":
                     XDDES,*91,"|","(",XFTAS,")",XASRC:
                     *98,"|","(",XFT1C,")":
                     XPICD,*111,"|","(",XFTAT,")",XATYP,*118,"|","(":
                     XFTAU,")",XAUDF,*125,"|","(",XFTDU,")",XDUDF
.
          IF         XFTPF<>0
            PRINT      *8,"|",*31,"|",*54,"|",*84,"|",*91,"|":
                       *98,"|","(",XFTPF,")":
                       XPIP1,*111,"|",*118,"|",*125,"|"
          ENDIF
.
          GOTO      PRNT1000
.
PRNT9000  CALL      UND132 
          PRINT     *N,*N,"WARNING - Inactive codes are printed on this report":
                    *N,*N,"Field type (0) = Ignored, (1) = Mandatory, ":
                          "(2) = Must be blank":
                    *N,*N,"*** END OF REPORT ***"
.
PRNT9999  RETURN
+
.**************************************************************************
.*                               MOVE0000              Called by: UPTM0000*
.*        Move variables to tempfile and write away                       *
.**************************************************************************
MOVE0000  MOVE      PMCCCCOD,XCCOD
          MOVE      PMCCHFUN,XHFUN
          MOVE      PMCCHFTY,XHFTY
          MOVE      PMCCCMCD,XCMCD
          MOVE      PMCCCNTR,XCCNT
          MOVE      PMCCPODY,XPODY
          MOVE      PMCCNPDY,XNPDY
          MOVE      PMCCHBPT,XHBPT
          MOVE      PMCCCCDY,XCCDY
          MOVE      PMCCFTDG,XFTDG
          MOVE      PMCCDDRG,XDDRG
          MOVE      PMCCFTCM,XFTCM
          MOVE      PMCCPCMF,XPCMF
          MOVE      PMCCPCMT,XPCMT
          MOVE      PMCCFTSC,XFTSC
          MOVE      PMCCSCMF,XSCMF
          MOVE      PMCCSCMT,XSCMT
          MOVE      PMCCFTTC,XFTTC
          MOVE      PMCCTCMF,XTCMF
          MOVE      PMCCTCMT,XTCMT
          MOVE      PMCCFTDS,XFTDS
          MOVE      PMCCDSTA,XDSTA
          MOVE      PMCCDST2,XDST2
          MOVE      PMCCDST3,XDST3
          MOVE      PMCCDST4,XDST4
          MOVE      PMCCDST5,XDST5
          MOVE      PMCCFTDD,XFTDD
          MOVE      PMCCDDES,XDDES
          MOVE      PMCCDDE2,XDDE2
          MOVE      PMCCDDE3,XDDE3
          MOVE      PMCCDDE4,XDDE4
          MOVE      PMCCDDE5,XDDE5
          MOVE      PMCCFTAS,XFTAS
          MOVE      PMCCASRC,XASRC
          MOVE      PMCCASRC,XASR2
          MOVE      PMCCASRC,XASR3
          MOVE      PMCCASRC,XASR4
          MOVE      PMCCASRC,XASR5
.
          MOVE      PMCCFT1C,XFT1C
          MOVE      PMCCPICD,XPICD
          MOVE      PMCCPIC2,XPIC2
          MOVE      PMCCPIC3,XPIC3
          MOVE      PMCCPIC4,XPIC4
          MOVE      PMCCPIC5,XPIC5
.
          MOVE      PMCCICPF,XFTPF
          MOVE      PMCCICP1,XPIP1
          MOVE      PMCCICP2,XPIP2
          MOVE      PMCCICP3,XPIP3
          MOVE      PMCCICP4,XPIP4
          MOVE      PMCCICP5,XPIP5
.
          MOVE      PMCCFTAT,XFTAT
          MOVE      PMCCATYP,XATYP
          MOVE      PMCCATY2,XATY2
          MOVE      PMCCATY3,XATY3
          MOVE      PMCCATY4,XATY4
          MOVE      PMCCATY5,XATY5
          MOVE      PMCCFTAU,XFTAU
          MOVE      PMCCAUDF,XAUDF
          MOVE      PMCCAUD2,XAUD2
          MOVE      PMCCAUD3,XAUD3
          MOVE      PMCCAUD4,XAUD4
          MOVE      PMCCAUD5,XAUD5
          MOVE      PMCCFTDU,XFTDU
          MOVE      PMCCDUDF,XDUDF
          MOVE      PMCCDUD2,XDUD2
          MOVE      PMCCDUD3,XDUD3
          MOVE      PMCCDUD4,XDUD4
          MOVE      PMCCDUD5,XDUD5
          MOVE      PMCCTYPE,XTYPE
          MOVE      PMCCCONT,XCONT
          MOVE      PMCCMIND,XMIND
.    
          MOVE      SP70,XSPAR
.
MOVE9999  RETURN
+
.**************************************************************************
.                               ERRO0000                                  *
.
.**************************************************************************
ERRO0000  MOVE      ONE,EXIT
ERRO9999  RETURN
+
.**************************************************************************
.*        IO Routines for tempfile                                        *
.**************************************************************************
RSTEMP1   READ      TEMP1,KEY34;;
          RETURN
.
RDTEMP1   MOVE      ZERO,OVRCD
          READ      TEMP1,KEY34;XCCOD,XHFUN,XHFTY,XCMCD,XCCNT,XPODY,XNPDY:
                                XHBPT,XCCDY,XFTDG,XDDRG,XFTCM,XPCMF,XPCMT:
                                XFTSC,XSCMF,XSCMT,XFTTC,XTCMF,XTCMT,XFTDS:
                                XDSTA,XDST2,XDST3,XDST4,XDST5,XFTDD,XDDES:
                                XDDE2,XDDE3,XDDE4,XDDE5,XFTAS,XASRC,XASR2:
                                XASR3,XASR4,XASR5,XFT1C,XPICD,XPIC2,XPIC3:
                                XPIC4,XPIC5,XFTAT,XATYP,XATY2,XATY3,XATY4:
                                XATY5,XFTAU,XAUDF,XAUD2,XAUD3,XAUD4,XAUD5:
                                XFTDU,XDUDF,XDUD2,XDUD3,XDUD4,XDUD5,XTYPE:
                                XCONT,XMIND,XSPAR,XITEM:
                                XFTPF,XPIP1,XPIP2,XPIP3,XPIP4,XPIP5
          GOTO      OVERCOND IF OVER
          RETURN
.
RKTEMP1   MOVE      ZERO,OVRCD
          READKS    TEMP1;XCCOD,XHFUN,XHFTY,XCMCD,XCCNT,XPODY,XNPDY:
                                XHBPT,XCCDY,XFTDG,XDDRG,XFTCM,XPCMF,XPCMT:
                                XFTSC,XSCMF,XSCMT,XFTTC,XTCMF,XTCMT,XFTDS:
                                XDSTA,XDST2,XDST3,XDST4,XDST5,XFTDD,XDDES:
                                XDDE2,XDDE3,XDDE4,XDDE5,XFTAS,XASRC,XASR2:
                                XASR3,XASR4,XASR5,XFT1C,XPICD,XPIC2,XPIC3:
                                XPIC4,XPIC5,XFTAT,XATYP,XATY2,XATY3,XATY4:
                                XATY5,XFTAU,XAUDF,XAUD2,XAUD3,XAUD4,XAUD5:
                                XFTDU,XDUDF,XDUD2,XDUD3,XDUD4,XDUD5,XTYPE:
                                XCONT,XMIND,XSPAR,XITEM:
                                XFTPF,XPIP1,XPIP2,XPIP3,XPIP4,XPIP5
          GOTO      OVERCOND IF OVER
          RETURN
.
RPTEMP1   MOVE      ZERO,OVRCD
          READKP    TEMP1;XCCOD,XHFUN,XHFTY,XCMCD,XCCNT,XPODY,XNPDY:
                                XHBPT,XCCDY,XFTDG,XDDRG,XFTCM,XPCMF,XPCMT:
                                XFTSC,XSCMF,XSCMT,XFTTC,XTCMF,XTCMT,XFTDS:
                                XDSTA,XDST2,XDST3,XDST4,XDST5,XFTDD,XDDES:
                                XDDE2,XDDE3,XDDE4,XDDE5,XFTAS,XASRC,XASR2:
                                XASR3,XASR4,XASR5,XFT1C,XPICD,XPIC2,XPIC3:
                                XPIC4,XPIC5,XFTAT,XATYP,XATY2,XATY3,XATY4:
                                XATY5,XFTAU,XAUDF,XAUD2,XAUD3,XAUD4,XAUD5:
                                XFTDU,XDUDF,XDUD2,XDUD3,XDUD4,XDUD5,XTYPE:
                                XCONT,XMIND,XSPAR,XITEM:
                                XFTPF,XPIP1,XPIP2,XPIP3,XPIP4,XPIP5
          GOTO      OVERCOND IF OVER
          RETURN
.
UPTEMP1   MOVE      ZERO,OVRCD
          UPDATE    TEMP1;XCCOD,XHFUN,XHFTY,XCMCD,XCCNT,XPODY,XNPDY:
                                XHBPT,XCCDY,XFTDG,XDDRG,XFTCM,XPCMF,XPCMT:
                                XFTSC,XSCMF,XSCMT,XFTTC,XTCMF,XTCMT,XFTDS:
                                XDSTA,XDST2,XDST3,XDST4,XDST5,XFTDD,XDDES:
                                XDDE2,XDDE3,XDDE4,XDDE5,XFTAS,XASRC,XASR2:
                                XASR3,XASR4,XASR5,XFT1C,XPICD,XPIC2,XPIC3:
                                XPIC4,XPIC5,XFTAT,XATYP,XATY2,XATY3,XATY4:
                                XATY5,XFTAU,XAUDF,XAUD2,XAUD3,XAUD4,XAUD5:
                                XFTDU,XDUDF,XDUD2,XDUD3,XDUD4,XDUD5,XTYPE:
                                XCONT,XMIND,XSPAR,XITEM:
                                XFTPF,XPIP1,XPIP2,XPIP3,XPIP4,XPIP5
          GOTO      OVERCOND IF OVER
          RETURN
.
WRTEMP1   MOVE      ZERO,OVRCD
          MOVE      XFITEM,XITEM
          WRITE     TEMP1,KEY34;XCCOD,XHFUN,XHFTY,XCMCD,XCCNT,XPODY,XNPDY:
                                XHBPT,XCCDY,XFTDG,XDDRG,XFTCM,XPCMF,XPCMT:
                                XFTSC,XSCMF,XSCMT,XFTTC,XTCMF,XTCMT,XFTDS:
                                XDSTA,XDST2,XDST3,XDST4,XDST5,XFTDD,XDDES:
                                XDDE2,XDDE3,XDDE4,XDDE5,XFTAS,XASRC,XASR2:
                                XASR3,XASR4,XASR5,XFT1C,XPICD,XPIC2,XPIC3:
                                XPIC4,XPIC5,XFTAT,XATYP,XATY2,XATY3,XATY4:
                                XATY5,XFTAU,XAUDF,XAUD2,XAUD3,XAUD4,XAUD5:
                                XFTDU,XDUDF,XDUD2,XDUD3,XDUD4,XDUD5,XTYPE:
                                XCONT,XMIND,XSPAR,XITEM:
                                XFTPF,XPIP1,XPIP2,XPIP3,XPIP4,XPIP5
          GOTO      OVERCOND IF OVER
          RETURN
.
DETEMP1   RESET     KEY34
          DELETE    TEMP1,KEY34
          RETURN
+
.**************************************************************************
.         I/O Includes                                                    *
.**************************************************************************
.
          INC       STD001IO
          INC       STDHLPCD
          INC       STDKWSCD
          INC       PATCODKY
          INC       PATCMCKY
          INC       PATDGWKY
          INC       PATFNDKY
.         INC       PATITMKY
          INC       TFILENAM                     * Generate Temp File Name
          INC       IBASEQIO/INC                 * Sequential Numbers File
          INC       PATCODIO/INC
          INC       PATCMCIO/INC
          INC       PMSCIDIO/INC
          INC       PATDGWIO/INC
          INC       PMSCMTIO/INC
          INC       PATFN1IO/INC
          INC       PATITMIO/INC
          INC       PATMI1IO/INC
          INC       PATKCOIO/INC      * ICD10 Operations Key Words
          INC       PATOICIO/INC      * ICD9 Operations key Words
          INC       PATKCDIO/INC      * ICD10 Diagnosis Key Words
          INC       PATDICIO/INC      * ICD9 Diagnosis Key Words
          INC       PATICDIO/INC      * ICD9 Diagnosis Key Words
          INC       PATICOIO/INC      * ICD9 Diagnosis Key Words
          INC       WEBERRIO/INC                 * Web Server Error Logging
