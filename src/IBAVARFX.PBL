.******************************************************************************
.* System    :   Common                                                       *
.* Program   :   IBAVARFX                                                     *
.* Desc      :                                                                *
.******************************************************************************
.* Date      :   19/05/2014                                                   *
.* Author    :   Peter Vela                                                   *
.* Function  :   This program will Transfer File and Load File (ibavaraf)     *
.*                                                                            *
.*    V10.14.01  15/04/2019  Davin         TSK 0838558                        *
.*               Removed delete of ibavarfx (TRANFILE) on program exit        *
.*               Fixed option = 0 (OPTN9500) to allow program exit            *
.*    V10.13.01  05/12/2018  Don Nguyen    TSK 0838558                        *
.*               Deleted temp file (TRANFILE) on program exit                 *
.*    V10.05.02  07/01/2015 Davin      CAR 300038                             *
.*               Check index 1 also to avoid I*D on description               *
.*    V10.05.01  05/11/2014 Davin      CAR 300038                             *
.*               Use index 2 to avoid I*D on description                      *
.*    V10.04.01  21/05/2014 Peter Vela CAR 300038                             *
.*               Created Program                                              *
.******************************************************************************
.
          INC       STD001FD
.
. FILE DESCRIPTION INCLUDES
. -------------------------
.
          INC      IBAVARFD/INC 
.
. LOCAL VARIABLE DEFINITION
. -------------------------
.
RECTYPE   FORM      1
TRANFILE  FILE      ASCII, FIXED=250
.
PRGID     INIT      "IBAVARFX"
PRGNAM    INIT      "IBAVARAF File Creation and Upload"
VERSION   INIT      "V11.03.00"
+
.**************************************************************************
.*                              ML0000                                    *
.*                      Controlling Logic (Mainline code)                 *
.**************************************************************************
.
ML0000    CALL      INIT0000               * initialisation and open files
.
ML0100    CALL      OPTN0000               * select options
          BRANCH    EXIT,ML9999            * EXIT = 1 if 0 chosen in menu
          BRANCH    COPTION,ML1000,ML2000  * proceed with clean up
.
ML1000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML3000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML2000    CALL      CONTQST                * Ok to continue ?
          BRANCH    CEXIT,ML4000:          * yes
                          ML0100:          * no
                          ML0100           * cancel
.
ML3000    PREP      TRANFILE,"ibavarfx"     
          CALL      MAKE0000               * Make Transfer File
          GOTO      ML9999
.
ML4000    MOVE      ZERO,OVRCD
          TRAP      OVERCOND IF IO
          OPEN      TRANFILE,"ibavarfx"     
          TRAPCLR   IO
          BRANCH    OVRCD,ML9900
          CALL      LOAD0000               * Load Transfer File
          GOTO      ML9999 
.
ML9900    DISPLAY   *P1:24,*EL,"Export File ibavarfx Not Found.";
          CALL      HITENTER
.
ML9999    CHAIN     PGM                    * chain back to program
.****************************************************************************
.*                                INIT0000             Called by: ML0000    *
.*  The initialisation routine is used to display headings and open files.  *
.****************************************************************************
.
INIT0000  CALL      DISPHEAD                  * display heading
          OPEN      IBAVARD1,"ibavaraf"
          OPEN      IBAVARD2,"ibavaraf"
.
INIT9999  RETURN
+
.
RDTRTR1   READ      TRANFILE,SEQ;DIBVRTYP,IBVRDESC,DIBVRFNM,IBVRFLEN,IBVRSPAR
          GOTO      OVERCOND IF OVER
          MOVE      DIBVRTYP,IBVRSTYP
          MOVE      DIBVRFNM,IBVRFNUM
          RETURN
.
WRTRTR1   WRITE     TRANFILE,SEQ;DIBVRTYP,IBVRDESC,DIBVRFNM,IBVRFLEN,IBVRSPAR
          RETURN
.
+
.****************************************************************************
.*                                OPTN0000             Called by: ML0000    *
.*                        get user options or exit                          *
.****************************************************************************
OPTN0000  MOVE       ZERO,CPAGENO      * very strange operation
          MOVE       "60",CLNO
.
          HLINE     *G37,2,52,80
          DISPLAY   *P1:3,*EF:
                    *P1:4,*V2LON,ZERO,*HOFF," = Exit":
                    *P1:5,*V2LON,ONE,*HOFF," = Make Transfer File":
                    *P1:6,*V2LON,TWO,*HOFF," = Load Transfer File":
                    *P1:8,"Select Option :";
.
OPTN1000  KEYIN     *P17:8,*EL,*DV,UNDLN1,*P17:8,*V2LON,COPTION;
.
          COMPARE   ZERO,COPTION
          GOTO      OPTN9500 IF EQUAL
.
          BRANCH    COPTION,OPTN1111,OPTN2222
          BEEP
          GOTO      OPTN1000
.
OPTN1111  DISPLAY   *P52:2,*V2LON,"- Make Transfer File ";
          MOVE      " Make Transfer File. ",CPHDROPT
          GOTO      OPTN9000

OPTN2222  DISPLAY   *P52:2,*V2LON,"- Load Transfer File ";
          MOVE      " Load Transfer File. ",CPHDROPT
.
OPTN9000  MOVE      ZERO,EXIT
          GOTO      OPTN9999
.
OPTN9500  MOVE      ONE,EXIT                     * exit program
OPTN9999  RETURN
+
.**************************************************************************
.*                               MAKE0000              Called by: ML0000  *
.*                     This makes the Transfer File                       *
.**************************************************************************
.
MAKE0000  PACK      KEY8,SP70
          CALL      RSIBVAR2
MAKE1000  CALL      RKIBVAR2
          BRANCH    OVRCD,MAKE9999
          CALL      WRTRTR1
          GOTO      MAKE1000
.
MAKE9999  RETURN
+
.**************************************************************************
.*                               LOAD0000              Called by: ML0000  *
.*                     This Loads the Transfer File                       *
.**************************************************************************
.
LOAD0000  MOVE      ZERO,OVRCD
          CALL      RDTRTR1
          BRANCH    OVRCD,LOAD9999
.
LOAD1000  PACK      KEY8,DIBVRTYP,DIBVRFNM,SP70
          CALL      RDIBVAR2                 
          IF        OVRCD=0
            CALL      UPIBVAR2
          ELSE
            PACK      KEY33,DIBVRTYP,IBVRDESC,SP70
            CALL      RAIBVAR1
            IF        OVRCD<>0
              CALL      WRIBVAR2
            ENDIF
          ENDIF
          GOTO      LOAD0000
.
LOAD9999  RETURN
+
. =========================================================================
.         I/O Includes
. =========================================================================
          INC      STD001IO
.
          INC      IBAVARIO/INC
