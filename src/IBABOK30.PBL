.******************************************************************************
.* System   :  BOOKINGS                                                       *
.* Program  :  IBABOK30                                                       *
.* Desc     :  ADHOC LETTER PRINTING                                          *
.******************************************************************************
.* Date     :  23/05/89                                                       *
.* Author   :  DIG                                                            *
.* Comment  :                                                                 *
.* Mods     :                                                                 *
.*                                                                            *
.******************************************************************************
.* V11.02.01 22/04/2022  Thanh T          TSK 090345                          *
.*           Added LXPXUCC4, LXUCC4LD, LXPXATF1, LXPXUCC5, LXUCC5LD and       *
.*           LXPXATF2 fields                                                  *
.******************************************************************************
.*        V10.13.01 05/12/2018  Don Nguyen       TSK 0838558                  *
.*                  Deleted temp file (TEMP1) on program exit                 *
.*        V10.06.01 17/09/2015   Davin           CAR 313906                   *
.*                  Added extra contacts to setup of postal address (post0000)*
.*        V10.04.02 07/07/2014   J.Tan           CAR 261630                   *
.*                  Removed port number from temp file name                   *
.*        V10.04.01 05/02/2014  Peter Vela 296699                             *
.*                  Fixed %addb, added %suburb, %addd                         *
.*                  Added %prodesc                                            *
.*        V9.03.01  10/06/2004  Lina Vo  CAR 50424                            *
.*                  Changed program for use by the web.                       *
.*        V9.03.00  02/07/2003  Tonii CAR 40346 (THC65)                       *
.*                  Ported from r5.10                                         *
.******************************************************************************
.*        V5.08.01  29/08/2000  Caleb Sharman                                 *
.*                  Changed Health Fund variables to be 8 chars               *
.*         V5.07.01   05/03/1999  Jim Stathopoulos                            *
.*                    Display Changes                                         *
.*         V5.07.B02  10/12/1998  Jim Stathopoulos                            *
.*                    CDATE Changes                                           *
.*                  22/10/1998  Glenn Berry      5.07 changes                 *
.******************************************************************************
.*          V5.04.02  11/04/1997  Steve Armstrong    SRF 642798               *
.*                    Fixed UK bug (not padding out work variable)            *
.*          V5.04.01  18/02/97 Howellsy     WHT                               *
.*                    Added Cell Phone, Postal Add1-4, Postal Post Code       *
.******************************************************************************
.*             V5.01 05/06/89 M.Ohleiter                                      *
.*                   Changed BOKMASFD                                         *
.*          V5.01.01 28/06/89 M.Ohleiter                                      *
.*                   Updated for release 5.01 (ur, adm, book nos)             *
.*                   Removed preferred accomodation - SRF 101630              *
.*                             14/08/89  S. O'Gorman                          *
.*                             Fix Modify Routine for > 80 Chars              *
.*          V5.01.02 12/12/89 K.Peak                                          *
.*                   Recompiled for 5th index in BOKMASFD SRF 103377          *
.*          V5.01.03 01/03/90 Steve O'Gorman                                  *
.*                   Reset UPPLOW variale after Scan in case conv.            *
.*          V5.01.04 24/01/91 Peter Eddey                                     *
.*                   Changed program to use BOKRC1FD instead of               *
.*                   BOKMASFD.                                                *
.*          V5.01.05 03/12/91 J.Tan      SRF 111890                           *
.*                   Extend field line number in letter file                  *
.*                                                                            *
.*                                                                            *
.******************************************************************************
          INC       STD001FD
          INC       IBASEQFD/INC            * Sequential Numbers File
          INC       TFILEVAR/INC            * Generate Temp File Name
          INC       WEBERRFD/INC            * Web Server Error Logging
.
          INC       BOKRC1FD/INC
          INC       BOKLETFD/INC
          INC       BOKLTLFD/INC
          INC       BOKLTSFD/INC
          INC       PATDO1FD/INC
          INC       PATCODFD/INC
          INC       PATCONFD/INC
          INC       PATMA1FD/INC
          INC       PMSPX2FD/INC
          INC       PATPR1FD/INC
          INC       PMSCEXFD/INC
          INC       WATPROFD/INC
+
.**********************************************************************
.*                             CONSTANTS                              *
.**********************************************************************
CODEBK    INIT      "BK"
CODEBP    INIT      "BP"
DEXT1     INIT      "st"
DEXT2     INIT      "nd"
DEXT3     INIT      "rd"
DEXT4     INIT      "th"
LCADDA    INIT      "%adda"             * Address Line 1      
LCADDB    INIT      "%addb"             * Address Line 2
LCADDC    INIT      "%addc"             * Address Line 3
LCDOB     INIT      "%bdob"             * Date of Birth
LCBDOC    INIT      "%bdoc"             * Attending Doctor's Name          *5
LCEXDATE  INIT      "%bedt"             * Expected Booking Date
LCGNAME   INIT      "%bgiv"             * Patient Given Name
LCBNO     INIT      "%bno"              * Booking Number
LCSNAME   INIT      "%bsur"             * Patient Surname          
LCBLTYPE  INIT      "%btype"            * Booking Type                     *10
LCPHONE   INIT      "%btele"            * Patient Phone Number
LCDATE    INIT      "%date"             * Today's Date
LCFNAME   INIT      "%fname"            * Patient Full Name
LCPCODE   INIT      "%post"             * Patient Post Code
LCPAGE    INIT      "%page"             * Page Break Variable              *15
LCCTELE   INIT      "%ctele"            * Cellular Phone Number
LCPADDA   INIT      "%padda"            * Postal address line 1
LCPADDB   INIT      "%paddb"            * Postal address line 2
LCPADDC   INIT      "%paddc"            * Postal address line 3
LCPADDD   INIT      "%paddd"            * Postal address line 4            *20
LCPPOST   INIT      "%ppost"            * Postal Post Code       
LCSUBRB   INIT      "%suburb"           * Suburb
LCADDD    INIT      "%addd"             * Address Line 4
LCPRDSC   INIT      "%prodesc"          * Procedure Description           
LCPXUCC4  INIT      "%idengend"         * Identify Gender Code             *25
LCUCC4LD  INIT      "%idgendsc"         * Identify Gender Long Description
LCPXATF1  INIT      "%idgentxt"         * Identify Gender Free Text
LCPXUCC5  INIT      "%pronoun"          * Identify Pronoun Code
LCUCC5LD  INIT      "%prondesc"         * Identify Pronoun Long Description
LCPXATF2  INIT      "%prontext"         * Identify Pronoun Free Text       *30
.
LXPAGE    INIT      "  -----              Page Break              -----  "
LXCELL    INIT      "XXXXXXXXXXXXXXXXXXXX"
LXPADDA   INIT      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
LXPADDB   INIT      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
LXPADDC   INIT      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
LXPADDD   INIT      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
LXPPOST   INIT      "XXXXXXXX"
LXUCC4LD  INIT      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
LXUCC5LD  INIT      "XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
.
MONTH1    INIT      "January"
MONTH2    INIT      "February"
MONTH3    INIT      "March"
MONTH4    INIT      "April"
MONTH5    INIT      "May"
MONTH6    INIT      "June"
MONTH7    INIT      "July"
MONTH8    INIT      "August"
MONTH9    INIT      "September"
MONTH10   INIT      "October"
MONTH11   INIT      "November"
MONTH12   INIT      "December"
REPSTR    INIT      "a0b0c0d0e0f0g0h0i0j0k0l0m0n0o0p0q0r0s0t0u0v0w0x0y0z0"
SPECCHAR  INIT      "%"
TO        INIT      " TO "
+
.**********************************************************************
.*                       GLOBAL VARIABLES                             *
.**********************************************************************
ADMFORM1  FORM      8
ADMFORM2  FORM      8
ADMTEST1  DIM       8
ADMTEST2  DIM       8
BOOKNUM   DIM       8
BOOKTYPE  DIM       20
BOTTMARG  FORM      2
CODE      DIM       2
COUNT     FORM      2
COUNTER   FORM      2
DATE      DIM       8
DATELINE  DIM       19
DEPPAID   DIM       1
DEXT      DIM       3
DIM30     DIM       30
DIM55     DIM       55
DIM70     DIM       70
DISPSTRN  DIM       70
DOCLINE   DIM       33
DOCTNAM   DIM       33
DUMMY     DIM       1
ENDSTR    FORM      2
FDATE     DIM       19
FIRSTCH   FORM      1
FLAGBOOK  FORM      1
FLAGDATE  FORM      1
FLAGDEPN  FORM      1
FLAGDEPY  FORM      1
FNAMER    DIM       8
FORM3     FORM      3
FORM3A    FORM      3
FULLNAME  DIM       52
INITIAL   DIM       4
LEFTMARG  FORM      2
LETDESC   DIM       25
LETTER    DIM       3
LETTFORM  FORM      3
LINE      DIM       55
BLETPLEN  FORM      3
MONTH     DIM       9
NAMELINE  DIM       52
PERCPOS   FORM      2
PHYSPAGE  FORM      3
POS       FORM      2
PROCDESC  DIM       80
PRTSTRNG  DIM       70
SELECTID  DIM       4
SRCHNUM   FORM      2
SRCHVAR   DIM       8
STARTSTR  FORM      2
TEMP2     FORM      2
TEMP3     FORM      3
TEMP55    DIM       55
TEMP70    DIM       70
TESTFORM  FORM      6
TOPMARG   FORM      2
URDIM     DIM       8
VERTPOS   FORM      2
WOPTION   FORM      1
XBDATE    DIM       19
.
PRGID     INIT      "IBABOK30"
PRGNAM    INIT      "Adhoc Letter Printing"
VERSION   INIT      "V11.03.00"
+
TEMP1     FILE      ASCII,FIXED=131
+
.**********************************************************************
.*                           MAINLINE                                 *
.*                      Controlling Logic                             *
.**********************************************************************
ML0000    CALL      INIT0000                      * display header,
          *         initialise files
          *         and set up port dependant
          *         temporary file
.
ML0200    CALL      CLRP0000                      * clear all input parameters
          CALL      OPTN0000                      * main option screen
          BRANCH    EXIT,ML9999
.
          BRANCH    OPTION,ML0600,ML0400
.
ML0400    CALL      BOOK0000                      *input single booking numbers
          BRANCH    EXIT,ML0200
          OPEN      TEMP1,FNAMER
          GOTO      ML0700
.
ML0600    MATCH     "IBARSH",PGM
          GOTO      ML5000 IF EQUAL
.
          CALL      DISP0000                      * display parameter screen
          CALL      SEL0000                       * select field or OK
.                                                    *   to post
          BRANCH    EXIT,ML0200
          CALL      EXTR0000                      * extract details
          BRANCH    EXIT,ML0200
.
ML0700    CALL      LSEL0000                      * select general letter
.                                                   *    for printing
ML0720    CALL      SET0000                       * set up values for % var's
          BRANCH    EXIT,ML0750
          CALL      PRIN0000                      * print letters
          BRANCH    EXIT,ML0700,ML0720
.
ML0750    CALL      MORE0000                      * any more letters to print
          BRANCH    EXIT,ML0200,ML0700
.
ML5000    CALL      WOPTN000                      * Display web options
          BRANCH    EXIT,ML9999
          BRANCH    WOPTION,ML5100,ML5200
.
ML5100    CALL      EXTR0000                      * extract details
          BRANCH    EXIT,ML0200
          GOTO      ML9999
.
ML5200    CALL      LSEL0000                      * select general letter
.                                                 *    for printing
ML5210    CALL      SET0000                       * set up values for % var's
          BRANCH    EXIT,ML0200
          CALL      PRIN0000                      * print letters
          BRANCH    EXIT,ML0200,ML5210
          GOTO      ML0200
.
ML9999    PREP      TEMP1,FNAMER
          CLOSE     TEMP1,DELETE
          CHAIN     PGM
+
.**********************************************************************
.*                           INIT0000                                 *
.*                   Display header and open files                    *
.**********************************************************************
INIT0000  CALL      DISPHEAD
          DISPLAY   *P51:24,"Opening  bokrc1af";
          OPEN      BOKRC1A1,"bokrc1af"
          OPEN      BOKRC1A4,"bokrc1af"
          DISPLAY   *P60:24,"bokletrf";
          OPEN      BOKLETR1,"bokletrf"
          OPEN      BOKLETR2,"bokletrf"
          DISPLAY   *P60:24,"patdo1af";
          OPEN      PATDO1A1,"patdo1af"
          OPEN      PATCODE1,"patcodes"
.
          DISPLAY   *P60:24,"patpramf";
          OPEN      PATPR1A1,"patpr1af"
          OPEN      PATPX1A1,"patpx1af"
.
          DISPLAY   *P60:24,"patma1af";
          OPEN      PATMA1A1,"patma1af"
.
          DISPLAY   *P60:24,"patmx1af";
          OPEN      PATMX1A1,"patmx1af"
.
          DISPLAY   *P60:24,"pmspx2af";
          OPEN      PMSPX2A1,"pmspx2af"
.
          OPEN      CONTROLF,"controlf"
          READ      CONTROLF,HUND18;*138,PTCNNEWC
.
          MATCH     "1",PTCNNEWC
          IF        @EQUAL
            DISPLAY   *P60:24,"pmscexaf";
            OPEN      PMSCEXA1,"pmscexaf"
          ENDIF
.
.------ set up port dependant temporary file ------
.
          CALL      TFILENAM                * Get temp file name
          MOVE      TFILNAME,FNAMER
.
          MATCH     "IBARSH",PGM
          GOTO      INIT9999 IF NOT EQUAL 
.
          DISPLAY   *P60:24,"bokltsaf";
          OPEN      BOKLTSA1,"bokltsaf"
          DISPLAY   *P60:24,"bokltlaf";
          OPEN      BOKLTLA1,"bokltlaf"
.
          DISPLAY   *P60:24,"watproaf";
          OPEN      WATPROA1,"watproaf"
          
INIT9999  RETURN
+
.**********************************************************************
.*                           OPTN0000                                 *
.*                   Display the main option screen                   *
.*  0 = Exit                                                          *
.*  1 = Print Letters                                                 *
.**********************************************************************
OPTN0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF,*V2LON,"0":
                    *P1:5,"1":
                    *P1:6,"2":
                    *P2:4,*HOFF,". Exit":
                    *P2:5,". Print Letters - Adhoc":
                    *P2:6,". Print Letters - Individual Bookings";
.
OPTN1000  DISPLAY   *P1:8,*EL,"Select Item : ";
          KEYIN     *P15:8,*V2LON,OPTION;
          COMPARE   ZERO,OPTION
          GOTO      OPTN9000 IF EQUAL
          COMPARE   THREE,OPTION
          GOTO      OPTN1000 IF NOT LESS
          GOTO      OPTN9999
.
.------ exit option chosen ------
.
OPTN9000  MOVE      TRUE,EXIT
.
OPTN9999  RETURN
+
.**********************************************************************
.*                           CLRP0000                                 *
.*                  Clears all input parameters                       *
.**********************************************************************
CLRP0000  MOVE      FALSE,FLAGDATE
          MOVE      FALSE,FLAGDEPN
          MOVE      FALSE,FLAGDEPY
          MOVE      FALSE,FLAGBOOK
          MOVE      SP1,DEPPAID
          MOVE      SP8,BKREBOOK
          MOVE      SP6,ADMTEST1
          MOVE      SP6,ADMTEST2
.
CLRP9999  RETURN
+
.**********************************************************************
.*                             DISP0000                               *
.*                    Display letter parameter screen                 *
.**********************************************************************
DISP0000  DISPLAY   *P1:4,*EF,*V2LON,ONE:
                    *P1:5,TWO:
                    *P2:4,*HOFF,". Bookings With Deposit Paid (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,") : ":
                    *P2:5,". Admission Date Range             : ";
.
DISP9999  RETURN
+
.**********************************************************************
.*                             SEL0000                                *
.*                Select field to modify or OK to post - General      *
.**********************************************************************
SEL0000   MOVE      FALSE,EXIT
          CALL      MAINQST
          COMPARE   ZERO,CCITEM                     * zero to post
          GOTO      SEL9999 IF EQUAL
          COMPARE   "-1",CCITEM                     * -1 to cancel
          GOTO      SEL9000 IF EQUAL
          COMPARE   THREE,CCITEM
          GOTO      SEL0000 IF NOT LESS             * invalid selection
          BRANCH    CCITEM,SEL1000,SEL2000
.
SEL1000   CALL      DEPS0000                        * input deposit
          GOTO      SEL0000
.
SEL2000   CALL      ADMS0000                        * input admission date
          GOTO      SEL0000
.
.------ cancel option chosen ------
.
SEL9000   MOVE      TRUE,EXIT
.
SEL9999   RETURN
+
.**********************************************************************
.*                             DEPS0000                               *
.*                   Input deposit paid field                         *
.**********************************************************************
DEPS0000  MOVE      TRUE,FLAGDEPY
          MOVE      FALSE,FLAGDEPN
          MOVE      SP1,DEPPAID
          DISPLAY   *P39:4,UNDLN1;
          KEYIN     *P39:4,*V2LON,*RV,DEPPAID;    * enter if deposits
          DISPLAY   *P39:4,*V2LON,DEPPAID;        *    are required
          MATCH     SP1,DEPPAID                   * test for spaces
          GOTO      DEPS8000 IF EQUAL
          MATCH     ANSN,DEPPAID                  * test for N entered
          GOTO      DEPS9000 IF EQUAL
          MATCH     ANSY,DEPPAID                  * test for Y entered
          GOTO      DEPS0000 IF NOT EQUAL
          DISPLAY   *P39:4,*V2LON,*EL,"Yes";
          GOTO      DEPS9999
.
.------ spaces entered ------
.
DEPS8000  MOVE      FALSE,FLAGDEPY
          MOVE      FALSE,FLAGDEPN
          DISPLAY   *P39:4,*EL;
          GOTO      DEPS9999
.
.------ No Deposits required ------
.
DEPS9000  MOVE      FALSE,FLAGDEPY
          MOVE      TRUE,FLAGDEPN
          DISPLAY   *P39:4,*V2LON,*EL,"No";
.
DEPS9999  RETURN
+
.**********************************************************************
.*                             BOOK0000                               *
.*                    Input a single booking number                   *
.**********************************************************************
BOOK0000  MOVE      FALSE,FLAGBOOK
          PREP      TEMP1,FNAMER
          MOVE      FALSE,EXIT
          DISPLAY   *P1:4,*EF,"Booking Number : ";
.
BOOK1000  MOVE      ZERO,BKREBOOK
          DISPLAY   *P18:4,*EL,UNDLN8;
          KEYIN     *P18:4,*V2LON,*RV,BKREBOOK;
          DISPLAY   *P18:4,*V2LON,BKREBOOK;
          COMPARE   ZERO,BKREBOOK
          GOTO      BOOK9900 IF EQUAL
          MOVE      BKREBOOK,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,BOOK9000
          CALL      EXTX0000                    * extract relevant details
          MOVE      TRUE,FLAGBOOK
          DISPLAY   *P1:24,*EL,"Details extracted ";
          CALL      HITENTER 
          DISPLAY   *P1:24,*EL;
          GOTO      BOOK1000
.
.------ invalid booking number entered ------
.
BOOK9000  DISPLAY   *P1:24,*EL,*B,"Booking Number not on file ";
          CALL      HITENTER 
          DISPLAY   *P1:24,*EL;
          GOTO      BOOK1000
.
BOOK9900  CLOSE     TEMP1
          BRANCH    FLAGBOOK,BOOK9999
          MOVE      TRUE,EXIT
.
BOOK9999  RETURN
+
.**********************************************************************
.*                             ADMS0000                               *
.*                    Input visit date range                          *
.**********************************************************************
ADMS0000  MOVE      "39",CCOL
          MOVE      ZERO,CHIGHLT
          MOVE      "5",CVERT
          MOVE      TRUE,FLAGDATE
          MOVE      SP2,CCENT
          MOVE      SP2,CDAY
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          DISPLAY   *P39:5,*EL;
          CALL      KEYDATE                        * keyin first date
          MATCH     SP2,CDAY                       * test for spaces entered
          GOTO      ADMS6000 IF EQUAL
          PACK      ADMTEST1,CCENT,CYEAR,CMON,CDAY
          MOVE      ADMTEST1,ADMFORM1
          MOVE      "53",CCOL
          DISPLAY   *P50:5,"To ";
          MOVE      SP2,CDAY
          MOVE      SP2,CMON
          MOVE      SP2,CYEAR
          MOVE      SP2,CCENT
          CALL      KEYDATE                        * keyin second date
          MATCH     SP2,CDAY
          GOTO      ADMS7000 IF EQUAL              * test for spaces entered
          PACK      ADMTEST2,CCENT,CYEAR,CMON,CDAY
          MOVE      ADMTEST2,ADMFORM2
          COMPARE   ADMFORM1,ADMFORM2              * test date range
          GOTO      ADMS7000 IF LESS
          GOTO      ADMS9999
.
.------ spaces entered for date ------
.
ADMS6000  DISPLAY   *P39:5,*EL;
          MOVE      FALSE,FLAGDATE
          GOTO      ADMS9999
.
.------ date range not valid ------
.
ADMS7000  DISPLAY   *P1:24,*EL,*B,"Invalid entry for date range ";
          CALL      HITENTER 
          DISPLAY   *P1:24,*EL;
          GOTO      ADMS0000
.
ADMS9999  RETURN
+
.**********************************************************************
.*                           EXTR0000                                 *
.*         Extracts the required information from various files       *
.*              for general letters                                   *
.**********************************************************************
EXTR0000  MOVE      FALSE,EXIT
          DISPLAY   *P45:24,"Searching";
.
          MATCH     "IBARSH",PGM
          IF        !@EQUAL
            PREP      TEMP1,FNAMER                   * sets up temporary file
          ENDIF
          MOVE      ONE,COUNT
          COMPARE   ZERO,FLAGDATE
          GOTO      EXTR0020 IF EQUAL
          PACK      KEY16,ADMTEST1,SP8
          CALL      RSBKREC4
          CALL      RKBKREC4                     * read booking file
          BRANCH    OVRCD,EXTR8500
          MATCH     BKREEDAT,ADMTEST2
          GOTO      EXTR8500 IF LESS
          GOTO      EXTR0100
.
EXTR0020  MOVE      SP20,KEY16
          CALL      RSBKREC4                     * read booking file
          CALL      RKBKREC4
          BRANCH    OVRCD,EXTR8500
          GOTO      EXTR0100
.
.------ read bookings file ------
.
EXTR0050  CALL      RKBKREC4
          BRANCH    OVRCD,EXTR8000
          COMPARE   ZERO,FLAGDATE
          GOTO      EXTR0100 IF EQUAL
          MATCH     BKREEDAT,ADMTEST2
          GOTO      EXTR8000 IF LESS
.
EXTR0100  MOVE      BKREBOOK,BOOKNUM
          DISPLAY   *P45:24,"Searching ",*V2LON,BKREBOOK;
.
          BRANCH    FLAGDEPY,EXTR1000
.
EXTR0110  BRANCH    FLAGDEPN,EXTR2000
.
EXTR0140  MATCH     "IBARSH",PGM
          IF        @EQUAL
            PACK      KEY12,SELECTID,BOOKNUM,SP70
            CALL      RDBKLTL1
            COMPARE   ZERO,OVRCD
            GOTO      EXTR0050 IF EQUAL
            MOVE      SELECTID,BKLLSEID
            MOVE      BOOKNUM,BKLLBOOK
            MOVE      SP1,BKLLSTAT
            MOVE      SP70,BKLLSPAR
            CALL      WRBKLTL1
          ELSE
            UNPACK    CDATE,CDAY,DUMMY,CMON,DUMMY,CCENT,CYEAR
            CALL      FDAT0000                        * format todays date
            MOVE      DATELINE,FDATE
            MOVE      "99",CDAY
            UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
            CALL      FDAT0000                        * format expected
            MOVE      DATELINE,XBDATE                 *   booking date
            CALL      FNAM0000                        * format patients name
            MOVE      NAMELINE,FULLNAME
            MOVE      BKREADOC,KEY6                    *    booking file
            CALL      FDOC0000                        * format doctors name
            MOVE      DOCLINE,DOCTNAM
.
.------ write formatted variables to temporary file ------
.
            WRITE     TEMP1,SEQ;BOOKNUM,FDATE,DOCTNAM,XBDATE,FULLNAME
          ENDIF
.
          DISPLAY   *P66:24,*EL,"Found ",*V2LON,BOOKNUM;
          ADD       ONE,COUNT
.
          GOTO      EXTR0050
.
.------ check bookings with deposits paid ------
.
EXTR1000  MATCH     DEPPAID,BKREPREA
          GOTO      EXTR0050 IF NOT EQUAL
          GOTO      EXTR0140
.
.------ check bookings with deposits not paid ------
.
EXTR2000  MATCH     DEPPAID,BKREPREA
          GOTO      EXTR0050 IF NOT EQUAL
          GOTO      EXTR0140
.
.------ all records have been found ------
.
EXTR8000  COMPARE   ONE,COUNT
          GOTO      EXTR8500 IF EQUAL
          DISPLAY   *P1:24,*EL,*B,"All records have been extracted ";
          CALL      HITENTER
.
          MATCH     "IBARSH",PGM
          IF        !@EQUAL
            CLOSE     TEMP1
            OPEN      TEMP1,FNAMER
          ENDIF
          GOTO      EXTR9999
.
.------ no records found ------
.
EXTR8500  DISPLAY   *P1:24,*EL,*B,"No records available for given parameters ";
          CALL      HITENTER
          MATCH     "IBARSH",PGM
          IF        !@EQUAL
            CLOSE     TEMP1
          ENDIF
          MOVE      TRUE,EXIT
.
EXTR9999  RETURN
+
.**********************************************************************
.*                               EXTX0000                             *
.*            Routine to extract details for a given booking number   *
.**********************************************************************
EXTX0000  UNPACK    CDATE,CDAY,DUMMY,CMON,DUMMY,CCENT,CYEAR
          CALL      FDAT0000                        * format todays date
          MOVE      DATELINE,FDATE
          MOVE      "99",CDAY
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      FDAT0000                        * format expected
          MOVE      DATELINE,XBDATE                 *    booking date
          CALL      FNAM0000                        * format patients name
          MOVE      NAMELINE,FULLNAME
          MOVE      BKREADOC,KEY6                   *    booking file
          CALL      FDOC0000                        * format doctors name
          MOVE      DOCLINE,DOCTNAM
          MOVE      BKREBOOK,BOOKNUM
.
.------ write formatted variables to temporary file ------
.
          WRITE     TEMP1,SEQ;BOOKNUM,FDATE,DOCTNAM,XBDATE,FULLNAME
.
EXTX9999  RETURN
+
.**********************************************************************
.*                           FDAT0000                                 *
.*                 Routine to format the date                         *
.**********************************************************************
FDAT0000  CLEAR     DATELINE
          MATCH     SP2,CDAY
          GOTO      FDAT7000 IF EQUAL
          MATCH     "99",CDAY
          GOTO      FDAT7000 IF EQUAL
          MOVE      CDAY,FORM2
.
.------ add date extension onto the day ------
.
          LOAD      DEXT,FORM2,DEXT1,DEXT2,DEXT3,DEXT4,DEXT4,DEXT4,DEXT4:
                    DEXT4,DEXT4,DEXT4,DEXT4,DEXT4,DEXT4,DEXT4:
                    DEXT4,DEXT4,DEXT4,DEXT4,DEXT4,DEXT4,DEXT1:
                    DEXT2,DEXT3,DEXT4,DEXT4,DEXT4,DEXT4,DEXT4:
                    DEXT4,DEXT4,DEXT1
          MOVE      CMON,FORM2
.
.------ change month into long format ------
.
          LOAD      MONTH,FORM2,MONTH1,MONTH2,MONTH3,MONTH4,MONTH5,MONTH6:
                    MONTH7,MONTH8,MONTH9,MONTH10,MONTH11,MONTH12
          RESET     CDAY
          CMATCH    "0",CDAY
          GOTO      FDAT1000 IF NOT EQUAL
          BUMP      CDAY
.
.------ pack the formatted date ------
.
FDAT1000  PACK      DATELINE,CDAY,DEXT,SP1,MONTH,SP1,CCENT,CYEAR
          MOVE      DATELINE,LINE
          CALL      COMP0000
          MOVE      LINE,DATELINE
          GOTO      FDAT9999
.
.------ date does not exist ------
.
FDAT7000  MOVE      "99th December 1999",DATELINE
          GOTO      FDAT9999
.
FDAT9999  RETURN
+
.**********************************************************************
.*                              FDOC0000                              *
.*                 Routine to format doctors name                     *
.**********************************************************************
FDOC0000  CLEAR     DOCLINE
          CALL      RDDOCT1                        * read doctor file
          BRANCH    OVRCD,FDOC7000
          CLEAR     INITIAL
          APPEND    SP1,INITIAL
          MOVE      DGNAME,ANS
          APPEND    ANS,INITIAL
          APPEND    ". ",INITIAL
          RESET     INITIAL
          PACK      DOCLINE,DTITL,INITIAL,DSNAME   * pack formatted doctors
          MOVE      DOCLINE,LINE                   *   name
          CALL      COMP0000                       * compress blanks
          CALL      CASE0000                       * convert to lower case
          MOVE      LINE,DOCLINE
          GOTO      FDOC9999
.
.------ doctors name does not exist ------
.
FDOC7000  MOVE      "*********************************",DOCLINE
          GOTO      FDOC9999
.
FDOC9999  RETURN
+
.**********************************************************************
.*                           FNAM0000                                 *
.*                 Routine to format patients name                    *
.**********************************************************************
FNAM0000  CLEAR     NAMELINE
          CALL      GPMI0000
          BRANCH    EXIT,FNAM9999
.
          RESET     PTITL
          MATCH     SP4,PTITL                     * test title for spaces
          GOTO      FNAM2000 IF EQUAL
.
FNAM0500  CMATCH    SP1,PTITL
          GOTO      FNAM1000 IF EQUAL
          MOVE      PTITL,ANS                    * append title to nameline
          APPEND    ANS,NAMELINE
          BUMP      PTITL
          GOTO      FNAM1000 IF EOS
          GOTO      FNAM0500
.
FNAM1000  APPEND    DOT,NAMELINE
          APPEND    SP1,NAMELINE
          MATCH     SP20,PGNAME                  * test given name for
          GOTO      FNAM1700 IF EQUAL            *    spaces
.
FNAM1500  APPEND    PGNAME,NAMELINE
          APPEND    SP1,NAMELINE                 * append given name to
.                                                   *    nameline
FNAM1700  APPEND    PSNAME,NAMELINE
          RESET     NAMELINE                     * append surname to
          MOVE      NAMELINE,LINE                *    nameline
          CALL      COMP0000                     * compress blanks
          CALL      CASE0000                     * convert to lower case
          MOVE      LINE,NAMELINE
          GOTO      FNAM9999
.
FNAM2000  MATCH     SP20,PGNAME                  * test given name for
          GOTO      FNAM1500 IF NOT EQUAL        *     spaces
          GOTO      FNAM1700
.
FNAM9999  RETURN
+
.**********************************************************************
.*                              COMP0000                              *
.*       Routine to compress excessive blanks in LINE                 *
.**********************************************************************
COMP0000  MOVE      LINE,DIM55
          PACK      TEMP55,SP20,SP20,SP10,SP5
          MATCH     TEMP55,DIM55                 * test line for spaces
          GOTO      COMP9999 IF EQUAL
          CLEAR     LINE
.
COMP1000  CMATCH    SP1,DIM55
          GOTO      COMP5000 IF NOT EQUAL        * compress leading blanks
          BUMP      DIM55
          GOTO      COMP1000 IF NOT EOS
          GOTO      COMP8000
.
COMP5000  MOVE      DIM55,TEMP55
          MOVE      TEMP55,DIM55
          ENDSET    DIM55
.
COMP5100  CMATCH    SP1,DIM55
          GOTO      COMP7000 IF NOT EQUAL        * compress trailing blanks
          BUMP      DIM55,-1
          GOTO      COMP5100
.
COMP7000  LENSET    DIM55
          RESET     DIM55
.
COMP7100  MOVE      DIM55,ANS
          APPEND    ANS,LINE
          BUMP      DIM55                        * compress multiple blanks
          GOTO      COMP8000 IF EOS              *   from in-between words
          CMATCH    SP1,DIM55
          GOTO      COMP7100 IF NOT EQUAL
          MOVE      DIM55,ANS
          APPEND    ANS,LINE
.
COMP7200  BUMP      DIM55
          CMATCH    SP1,DIM55
          GOTO      COMP7100 IF NOT EQUAL
          GOTO      COMP7200
.
COMP8000  RESET     LINE
          GOTO      COMP9999
.
COMP9999  RETURN
+
.**********************************************************************
.*                             CASE0000                               *
.*  Routine to convert all except first character of each word to     *
.*        lower case                                                  *
.**********************************************************************
CASE0000  MOVE      ONE,FIRSTCH
          RESET     LINE
.
CASE1000  CMATCH    SP1,LINE
          GOTO      CASE5000 IF EQUAL            * match char to space
          BRANCH    FIRSTCH,CASE2000
          MOVE      LINE,ANS
          RESET     UPPLOW
          SCAN      ANS,UPPLOW                   * test to see if char is
          GOTO      CASE1100 IF EQUAL            *   a letter
          MOVE      ONE,FIRSTCH
          GOTO      CASE3000                     * any char but a letter
.                                                *         found
.
CASE1100  RESET     UPPLOW
          OR        040,LINE                     * actual conversion to
.                                                * lower case (believe it
.                                                *             or not!!!)
.
CASE2000  MOVE      ZERO,FIRSTCH                 * not on first char any
.                                                   *   more
CASE3000  BUMP      LINE
          GOTO      CASE1000 IF NOT EOS          * move to next char
          RESET     LINE                         * end of line reached
          GOTO      CASE9999
.
CASE5000  MOVE      ONE,FIRSTCH                  * first char was a space
          GOTO      CASE3000
.
CASE9999  RETURN
+
.**********************************************************************
.*                              LSEL0000                              *
.*                Select general letter to print                      *
.**********************************************************************
LSEL0000  MOVE      FALSE,EXIT
          MOVE      ZERO,COUNTER
          DISPLAY   *P1:4,*EF:
                    *P1:5,"Select Letter to Print : ";
          MOVE      SP3,LETTER
          DISPLAY   *P26:5,UNDLN3;
          KEYIN     *P26:5,*V2LON,*RV,LETTER;       * enter letter code
          DISPLAY   *P26:5,*V2LON,LETTER;
          MATCH     SP3,LETTER                      * Check for spaces
          GOTO      LSEL7000 IF EQUAL
          MATCH     "0",LETTER                      * check for zero
          GOTO      LSEL7000 IF EQUAL
          MATCH     QUEST,LETTER                    * Check for a "?"
          GOTO      LSEL1000 IF NOT EQUAL
          GOTO      LSEL5000                        * Display LETTER fields
.
.------ LETTER field entered ------
.
LSEL1000  MOVE      LETTER,LETTFORM
          MOVE      ZERO,FORM3
          PACK      KEY6,LETTFORM,FORM3         * Validate letter is on the
          CALL      RDLET1                      * letter file
          COMPARE   ONE,OVRCD
          GOTO      LSEL1200 IF NOT EQUAL
.
.------ letter not on file ------
.
          DISPLAY   *P1:24,"LETTER code not on file ";
          CALL      HITENTER
          GOTO      LSEL0000                  * return if not found
.
LSEL1200  MOVE      BLTEXT,LETDESC
          DISPLAY   *P31:5,LETDESC;           * Display the description
          GOTO      LSEL9999
.
.------ ? has been entered for LETTER field ------
.
LSEL5000  MOVE      SP10,KEY6
          CALL      RDSLET2
.
.         Display Category Heading, and then loop over entries
.
          DISPLAY   *P1:4,*EF,*P30:4,*V2LON,*ULON,"EXISTING LETTERS"
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
.
.>>>>>>>  TRAP      LSEL5300 IF F1
.
LSEL5100  CALL      RDKLET2
          BRANCH    OVRCD,LSEL5900
.
          COMPARE   ZERO,BLINNO
          GOTO      LSEL5400 IF NOT EQUAL
.
          MOVE      BLTEXT,DIM30
          DISPLAY   *PCCOL:CVERT,*V2LON,BLETNO,SP2,*HOFF,DIM30;
          ADD       ONE,CVERT
          COMPARE   TWENTY3,CVERT
          GOTO      LSEL5100 IF LESS
.
          MOVE      FIVE,CVERT
          ADD       "40",CCOL
          COMPARE   "70",CCOL
          GOTO      LSEL5100 IF LESS
.
.>>>>>>>  TRAPCLR   F1
.
          KEYIN     *P1:24,*EL," (",*V2LON,"E",*HOFF,")xit, (":
                    *V2LON,"N",*HOFF,")ext Screen : ",*V2LON,ANS;
.
          CMATCH    ANSE,ANS
          GOTO      LSEL0000 IF EQUAL
.
          MOVE      FIVE,CVERT
          MOVE      ONE,CCOL
          DISPLAY   *P1:5,*EF
.
.>>>>>>>  TRAP      LSEL5300 IF F1
.
          GOTO      LSEL5100
.
.
LSEL5300  NORETURN
.>>>>>>>  TRAPCLR   F1
.
.      End of codes
.
LSEL5400  DISPLAY   *P1:24,*EL;
          CALL      HITENTER
          GOTO      LSEL0000
.
LSEL5900  DISPLAY   *P1:24,*EF,"No letters Entered in letter File ";
          CALL      HITENTER
          GOTO      LSEL0000
.
.------ zero entered for letter code, used as an exit char ------
.
LSEL7000  DISPLAY   *P1:24,"Are you sure you want to exit? (",*V2LON,"Y":
                    *HOFF,"/",*V2LON,"N",*HOFF,")";
          KEYIN     *P38:24,*V2LON,ANS;
          TYPE      ANS
          GOTO      LSEL7000 IF EQUAL
          REPLACE   "Y1N0",ANS
          TYPE      ANS
          GOTO      LSEL7000 IF NOT EQUAL              * invalid option
          MOVE      ANS,OPTION
          BRANCH    OPTION,LSEL7500
          GOTO      LSEL0000                           * re-enter letter code
.
.------ user wants to exit, branch back to main menu ------
.
LSEL7500  NORETURN
          GOTO      ML0200
.
LSEL9999  RETURN
+
.**********************************************************************
.*                              SET0000                               *
.*             Set up the values for all the "%" variables            *
.**********************************************************************
.
.------ read the temporary file ------
.
SET0000   MATCH     "IBARSH",PGM
          IF        @EQUAL
            BRANCH    OPTION,SET0010,SET0020
          ENDIF
          GOTO      SET0020 
.
SET0010   CALL      RKBKLTL1                     
          BRANCH    OVRCD,SET9000
          MATCH     SELECTID,BKLLSEID
          GOTO      SET9000 IF NOT EQUAL
          MATCH     "1",BKLLSTAT
          GOTO      SET0010 IF EQUAL
          MOVE      BKLLBOOK,BOOKNUM
          GOTO      SET0030 
.
SET0020   READ      TEMP1,SEQ;BOOKNUM,FDATE,DOCTNAM,XBDATE,FULLNAME
          GOTO      SET9000 IF OVER
          GOTO      SET0030 
.
SET0030   CALL      CLRV0000                             * initialise var's
          DISPLAY   *P60:24,*EL,*V2LON,COUNTER,*HOFF," Printed";
          MOVE      FALSE,EXIT
.
.------ get values of other % variables ------
.------   not already formatted -------
.
          ADD       ONE,COUNTER
          MOVE      FALSE,EXIT
          MOVE      BOOKNUM,KEY8
          CALL      RDBKREC1
          BRANCH    OVRCD,SET9000
.
          MATCH     "IBARSH",PGM
          IF        @EQUAL
            BRANCH    OPTION,SET0040,SET0050
          ENDIF
          GOTO      SET0050
.
SET0040   UNPACK    CDATE,CDAY,DUMMY,CMON,DUMMY,CCENT,CYEAR
          CALL      FDAT0000                        * format todays date
          MOVE      DATELINE,FDATE
          MOVE      "99",CDAY
          UNPACK    BKREEDAT,CCENT,CYEAR,CMON,CDAY
          CALL      FDAT0000                        * format expected
          MOVE      DATELINE,XBDATE                 *   booking date
          CALL      FNAM0000                        * format patients name
          MOVE      NAMELINE,FULLNAME
          MOVE      BKREADOC,KEY6                    *    booking file
          CALL      FDOC0000                        * format doctors name
          MOVE      DOCLINE,DOCTNAM
          GOTO      SET0100
.
SET0050   CALL      GPMI0000
.          GOTO      SET0100
.
SET0100   
.         MATCH     "                         ",PADD1
.          GOTO      SET0110 IF NOT EQUAL
.         MOVE      "*************************",PADD1
.
.SET0110   MATCH     "                         ",PADD2
.          GOTO      SET0130 IF NOT EQUAL
.          MOVE      "*************************",PADD2
.
.SET0130   MATCH     "    ",PPOST
.          GOTO      SET0140 IF NOT EQUAL
.          MOVE      "****",PPOST
.
.SET0140   MATCH     "                         ",PGNAME
.          GOTO      SET0150 IF NOT EQUAL
.          MOVE      "*************************",PGNAME
.
.SET0150   MATCH     "                   ",PSNAME
.          GOTO      SET0155 IF NOT EQUAL
.          MOVE      "*******************",PSNAME
.
.SET0155   MATCH     "            ",PTELEP
.          GOTO      SET0160 IF NOT EQUAL
.          MOVE      "************",PTELEP
.
SET0160   UNPACK    PBDATE,CCENT,CYEAR,CMON,CDAY
          MATCH     SP2,CDAY
          GOTO      SET0300 IF NOT EQUAL
          MOVE      "99",CDAY
          MOVE      "99",CMON
          MOVE      "99",CYEAR
.
SET0300   PACK      DATE,CDAY,SLASH,CMON,SLASH,CYEAR
          MATCH     SP3,BKRETYPE
          GOTO      SET0400 IF EQUAL
.
          PACK      KEY5,CODEBK,BKRETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,SET0400
          MOVE      TDESC,BOOKTYPE
          GOTO      SET0460
.
SET0400   MOVE      "                    ",BOOKTYPE
.
.     Omitted preferred accomodation 
.
.SET0420   MATCH     SP3,BKREACCM
.          GOTO      SET0450 IF EQUAL
.          PACK      KEY5,CODEBP,BKREACCM
.          CALL      RDCODE1
.          BRANCH    OVRCD,SET0450
.          MOVE      TDESC,PACCOM
.          GOTO      SET0500
.
.SET0450   MOVE      "********************",PACCOM
.
SET0460   MATCH     SP70,BKREPROC   
          GOTO      SET0490 IF EQUAL
.
          PACK      KEY9,BKREPROC,SP70
          CALL      RDPROA1
          BRANCH    OVRCD,SET0490
.
          MOVE      WPDESC,PROCDESC
          GOTO      SET0500
.
SET0490   PACK      PROCDESC,SP70,SP70
.
.------ move all names, addresses etc to LINE and convert to lower case ------
.
SET0500   CALL      POST0000                     * set postal address
          MOVE      PADD1,LINE
          CALL      CASE0000
          MOVE      LINE,PADD1
          MOVE      PADD2,LINE
          CALL      CASE0000
          MOVE      LINE,PADD2
          MOVE      PSUBRB,LINE
          CALL      CASE0000
          MOVE      LINE,PSUBRB
          MOVE      PADD4,LINE
          CALL      CASE0000
          MOVE      LINE,PADD4
          MOVE      PGNAME,LINE
          CALL      CASE0000
          MOVE      LINE,PGNAME
          MOVE      PSNAME,LINE
          CALL      CASE0000
          MOVE      LINE,PSNAME
          MOVE      BOOKTYPE,LINE
          CALL      CASE0000
          MOVE      LINE,BOOKTYPE
          MOVE      PTMXCELL,LINE              * load cellular phone number
          CALL      CASE0000
          MOVE      LINE,LXCELL
          MOVE      PTMXADD1,LINE              * load postal address line 1
          CALL      CASE0000
          MOVE      LINE,LXPADDA
          MOVE      PTMXADD2,LINE              * load postal address line 2
          CALL      CASE0000
          MOVE      LINE,LXPADDB
          MOVE      PTMXADD3,LINE              * load postal address line 3
          CALL      CASE0000
          MOVE      LINE,LXPADDC
          MOVE      PTMXADD4,LINE              * load postal address line 4
          CALL      CASE0000
          MOVE      LINE,LXPADDD
          MOVE      PTMXPOST,LINE              * load postal Post Code     
          CALL      CASE0000
          MOVE      LINE,LXPPOST
.
          MATCH     SP70,PMPXUCC4
          IF        !@EQUAL
            MOVE      "Gi",DIM2
            PACK      KEY5,DIM2,PMPXUCC4,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      PTCDLDES,LXUCC4LD
            ENDIF
          ENDIF
.
          MATCH     SP70,PMPXUCC5
          IF        !@EQUAL
            MOVE      "Gp",DIM2
            PACK      KEY5,DIM2,PMPXUCC5,SP70
            CALL      RDCODE1
            IF        OVRCD=0
              MOVE      PTCDLDES,LXUCC5LD
            ENDIF
          ENDIF
.
.          MOVE      PACCOM,LINE
.          CALL      CASE0000
.          MOVE      LINE,PACCOM
          GOTO      SET9999
.
SET9000   MOVE      TRUE,EXIT
.
SET9999   RETURN
+
.**********************************************************************
.*                              CLRV0000                              *
.*      Routine to initialise all unformatted variables to "*"        *
.**********************************************************************
CLRV0000  MOVE      "                         ",PADD1
          MOVE      "                         ",PADD2
          MOVE      "                         ",PSUBRB
          MOVE      "                         ",PADD4
          MOVE      "                         ",PGNAME
          MOVE      "                    ",PSNAME
          MOVE      "    ",PPOST
          MOVE      "            ",PTELEP
          MOVE      "                    ",LXCELL
          MOVE      "                                   ",LXPADDA
          MOVE      "                                   ",LXPADDB
          MOVE      "                                   ",LXPADDC
          MOVE      "                                   ",LXPADDD
          MOVE      "        ",LXPPOST
          MOVE      SP70,LXUCC4LD
          MOVE      SP70,LXUCC5LD
.
CLRV9999  RETURN
+
.**********************************************************************
.*                              PRIN0000                              *
.*             Set up and print letter one line at a time             *
.**********************************************************************
PRIN0000  MOVE      FALSE,EXIT
          MOVE      ONE,COUNT
          MOVE      LETTER,LETTFORM
          MOVE      ZERO,FORM3                   * read letter file header
          PACK      KEY6,LETTFORM,FORM3          *    record
          CALL      RDLET1
          MOVE      BLETPPAG,PHYSPAGE
          MOVE      BLETPTOP,TOPMARG
          MOVE      BLETPBOT,BOTTMARG            * set up margins etc.
          SUBTRACT  BLETPBOT,BLETPPAG
          MOVE      BLETPPAG,BLETPLEN
          MOVE      BLETPTAB,LEFTMARG
.
.------ print top margin ------
.
PRIN0050  PRINT     *N;
          COMPARE   COUNT,TOPMARG
          GOTO      PRIN1000 IF EQUAL
          ADD       ONE,COUNT
          GOTO      PRIN0050
.
.------ print letter one line at a time ------
.
PRIN1000  CALL      RDKLET1
          BRANCH    OVRCD,PRIN9000               * end of letter reached
          COMPARE   LETTFORM,BLETNO
          GOTO      PRIN9000 IF NOT EQUAL
          BRANCH    BLVAR,PRIN2000
          PRINT     *N,*LEFTMARG,BLTEXT;         * print text if no % vars
          ADD       ONE,COUNT
          COMPARE   COUNT,BLETPLEN
          CALL      PAGE0000 IF EQUAL            * page when required
          GOTO      PRIN1000
.
PRIN2000  CALL      MOD0000                      * substitute % var value
          PRINT     *N,*LEFTMARG,PRTSTRNG;       * print text with % vars
          ADD       ONE,COUNT
          COMPARE   COUNT,BLETPLEN
          CALL      PAGE0000 IF EQUAL            * page when required
          GOTO      PRIN1000
.
PRIN9000  COMPARE   TOPMARG,COUNT
          GOTO      PRIN9500 IF EQUAL
          MOVE      TWO,EXIT
          DISPLAY   *P60:24,*EL,*V2LON,COUNTER,*HOFF," Printed";
          MOVE      PHYSPAGE,TEMP3                * set up variables for
          SUBTRACT  COUNT,PHYSPAGE                *   paging
          MOVE      ONE,COUNT
.
.------ paginate at end of each letter ------
.
PRIN9100  PRINT     *N;
          COMPARE   COUNT,PHYSPAGE
          GOTO      PRIN9400 IF EQUAL
          ADD       ONE,COUNT
          GOTO      PRIN9100
.
PRIN9400  MOVE      TEMP3,PHYSPAGE
          GOTO      PRIN9999
.
.------ letter header is on file but nothing else ------
.
PRIN9500  DISPLAY   *P1:24,*EL,"Letter has not been set up yet ";
          CALL      HITENTER
          MATCH     "IBARSH",PGM
          IF        @EQUAL
            BRANCH    OPTION,PRIN9510
          ENDIF
.
          CLOSE     TEMP1
          OPEN      TEMP1,FNAMER
.
PRIN9510  MOVE      TRUE,EXIT
.
PRIN9999  RETURN
+
.**********************************************************************
.*                              PAGE0000                              *
.*        Routine to paginate if a new page is required               *
.**********************************************************************
PAGE0000  MOVE      BLETNO,FORM3
          MOVE      BLINNO,FORM3A
          CALL      RDKLET1
          BRANCH    OVRCD,PAGE8000                * check to see that
          COMPARE   LETTFORM,BLETNO               *  another line of the
          GOTO      PAGE8000 IF NOT EQUAL         *  letter exists
          MOVE      TOPMARG,TEMP2
          ADD       BOTTMARG,TOPMARG
          MOVE      ONE,COUNT
.
.------ loop to paginate ------
.
PAGE1000  PRINT     *N;
          COMPARE   COUNT,TOPMARG
          GOTO      PAGE9000 IF EQUAL
          ADD       ONE,COUNT
          GOTO      PAGE1000
.
.------ no need to paginate ------
.
PAGE8000  PACK      KEY6,FORM3,FORM3A
          CALL      RDLET1
          GOTO      PAGE9999
.
.------ reset read to previous record to continue processing ------
.
PAGE9000  MOVE      TEMP2,COUNT
          MOVE      TEMP2,TOPMARG
          PACK      KEY6,FORM3,FORM3A
          CALL      RDLET1
.
PAGE9999  RETURN
+
.**********************************************************************
.*                              MOD0000                               *
.*             Modify a line to substitute "%" variable values        *
.**********************************************************************
MOD0000   MOVE      BLTEXT,DIM70
          PACK      PRTSTRNG,SP30,SP30,SP10          * initialise PRTSTRNG
          RESET     PRTSTRNG,0                       *   and pointer positions
          MOVE      ONE,STARTSTR
          MOVE      ONE,ENDSTR
.
MOD2000   SCAN      SPECCHAR,DIM70                * Scan the Input Line for
          GOTO      MOD8000 IF NOT EQUAL          * a "%" sign
.
MOD3000   MOVEFPTR  DIM70,PERCPOS
          BUMP      DIM70,-1                     * Go to the character before
.                                                * the "%" sign
          GOTO      MOD3100 IF NOT EOS            * Test if the % sign is the
          RESET     DIM70,0                      * first character on the line
.                                                * set f.p. to 0 if it is
.
MOD3100   MOVEFPTR  DIM70,ENDSTR                  * store endtsr at position
          COMPARE   STARTSTR,ENDSTR               *   1 char before the % sign
          GOTO      MOD3200 IF NOT LESS           *   and ensure that end and
.                                                *   start positions dont
.                                                *   overlap
          RESET     DIM70,STARTSTR                * set pointer to next % sign
          GOTO      MOD9999 IF EOS                *   if end < start
          GOTO      MOD3300
.
MOD3200   RESET     DIM70,STARTSTR                * reset the pointers to
          SETLPTR   DIM70,ENDSTR                  *   extract text preceding
          APPEND    DIM70,PRTSTRNG                *   the % sign
.
MOD3300   SETLPTR   DIM70,70                      * set the pointers to start at
          RESET     DIM70,PERCPOS                 *    % and go to position 70
          MOVE      DIM70,TEMP70                  * delete preceding text from
          PACK      DIM70,TEMP70,SP30,SP30,SP30   *    the DIM70 string
.
MOD4000   CALL      GETV0000                      * extract the % var
          MOVEFPTR  DIM70,STARTSTR                * store finish pos. of var
          BRANCH    EXIT,MOD5000
          RESET     SRCHVAR
.
.------ search for the % var in the list of constants ------
.
          SEARCH    SRCHVAR,LCADDA,THIRTY,SRCHNUM
          BRANCH    SRCHNUM,MOD4500,MOD4500,MOD4500,MOD4500,MOD4500:
                    MOD4500,MOD4500,MOD4500,MOD4500,MOD4500,MOD4500:
                    MOD4500,MOD4500,MOD4500,MOD7000,MOD4500,MOD4500:
                    MOD4500,MOD4500,MOD4500,MOD4500
.
MOD4500   COMPARE   ZERO,SRCHNUM                   * % var not found in list
          GOTO      MOD5000 IF EQUAL
.
.------ load DISPSTRN with the actual value of the % var ------
.
          LOAD      DISPSTRN,SRCHNUM,PADD1,PADD2,PSUBRB,DATE,DOCTNAM:     *5
                    XBDATE,PGNAME,BOOKNUM,PSNAME,BOOKTYPE:                *10
                    PTELEP,FDATE,FULLNAME,PPOST,LXPAGE:                   *15
                    LXCELL,LXPADDA,LXPADDB,LXPADDC,LXPADDD:               *20 
                    LXPPOST,PSUBRB,PADD4,PROCDESC,PMPXUCC4:               *25
                    LXUCC4LD,PMPXATF1,PMPXUCC5,LXUCC5LD,PMPXATF2          *30 
.
          APPEND    DISPSTRN,PRTSTRNG
          CALL      COMX0000                   * compress trailing blanks in
          GOTO      MOD2000                    *    PRTSTRNG
.
MOD5000   RESET     SRCHVAR                    * add the string starting with
          APPEND    SRCHVAR,PRTSTRNG           *    a % if not found in the
          GOTO      MOD8000 IF EOS             *    list of constants 
          GOTO      MOD2000       
.
.------ % page variable found ------
.
MOD7000   MOVE      PHYSPAGE,TEMP3
          SUBTRACT  COUNT,PHYSPAGE
          MOVE      ONE,COUNT
          ADD       TOPMARG,PHYSPAGE
          SUBTRACT  ONE,PHYSPAGE
.
MOD7100   PRINT     *N;
          COMPARE   COUNT,PHYSPAGE
          GOTO      MOD7500 IF EQUAL
          ADD       ONE,COUNT
          GOTO      MOD7100
.
MOD7500   MOVE      TEMP3,PHYSPAGE
          MOVE      TOPMARG,COUNT
          SUBTRACT  ONE,COUNT
          PACK      PRTSTRNG,SP30,SP30,SP10
          GOTO      MOD9999
.
MOD8000   APPEND    DIM70,PRTSTRNG                    * add remaining text to
          RESET     PRTSTRNG                          *   PRTSTRNG if no %
.                                                    *   signs left
MOD9999   RETURN
+
.**********************************************************************
.*                              GETV0000                              *
.*           Extracts the "%" variable from the line                  *
.**********************************************************************
GETV0000  MOVE      FALSE,EXIT
          PACK      SRCHVAR,SP30,SP30,SP10
          RESET     SRCHVAR,0
          MOVE      DIM70,ANS
          APPEND    ANS,SRCHVAR
          RESET     SRCHVAR,0
          BUMP      DIM70
          MOVE      DIM70,ANS
          REPLACE   REPSTR,ANS                        * check to see if first
          MATCH     "0",ANS                           *   char after % is a
          GOTO      GETV9000 IF NOT EQUAL             *   lower case letter
          BUMP      DIM70,-1
          PACK      SRCHVAR,SP30,SP30,SP10            * initialise SRCHVAR
          RESET     SRCHVAR,0                         *   value and pointers
.
.------ append % var to SRCHVAR until anything but ------
.------ a lower case letter is found ------
.
GETV1000  MOVE      DIM70,ANS
          APPEND    ANS,SRCHVAR
.
          BUMP      DIM70
          GOTO      GETV9999 IF EOS
          MOVE      DIM70,ANS
          REPLACE   REPSTR,ANS
          MATCH     "0",ANS
          GOTO      GETV9999 IF NOT EQUAL
          GOTO      GETV1000
.
.------ string found is not a valid % var ------
.
GETV9000  MOVE      TRUE,EXIT
.
GETV9999  RETURN
+
.**********************************************************************
.*                              COMX0000                              *
.*         Routine to compress trailing blanks in PRTSTRNG            *
.**********************************************************************
COMX0000  ENDSET    PRTSTRNG
.
COMX1000  CMATCH    SP1,PRTSTRNG
          GOTO      COMX9999 IF NOT EQUAL
          BUMP      PRTSTRNG,-1
          GOTO      COMX9999 IF EOS
          GOTO      COMX1000
.
COMX9999  RETURN
+
.**********************************************************************
.*                               MORE0000                             *
.*                  Any more letters to print?                        *
.**********************************************************************
MORE0000  MOVE      FALSE,EXIT
          DISPLAY   *P1:7,*EF,"Any more letters for the same people? (":
                    *V2LON,"Y",*HOFF,"/",*V2LON,"N",*HOFF,")";
          KEYIN     *P44:7,*V2LON,ANS;
          TYPE      ANS
          GOTO      MORE0000 IF EQUAL
          REPLACE   "Y1N0",ANS
          TYPE      ANS
          GOTO      MORE0000 IF NOT EQUAL
          MOVE      ANS,OPTION
          BRANCH    OPTION,MORE1000
          MOVE      TRUE,EXIT                     * no more letters desired
          GOTO      MORE9999
.
.------ there are more letters to print ------
.
MORE1000  MOVE      TWO,EXIT
          CLOSE     TEMP1
          OPEN      TEMP1,FNAMER
          GOTO      MORE9999
.
MORE9999  RETURN
+
.
.
. **********************************************************************
. * GPMI0000 : Get the PMI details for a booking.                      *
. **********************************************************************
.
GPMI0000  MATCH     ZEROUR,BKREURNO
          GOTO      GPMI1000 IF EQUAL
.
          MOVE      BKREURNO,PURNO
          MOVE      PURNO,KEY8
          CALL      RDMAST1
          BRANCH    OVRCD,GPMI1000
.
          PACK      KEY8,PURNO,SP70
          CALL      RDPMPX21
          IF        OVRCD = 1
            CALL     CLPMSPX2
          ENDIF
.
          GOTO      GPMI8000
.
GPMI1000  MOVE      BKREBOOK,PURNO
          MOVE      PURNO,KEY8
          CALL      RDPRAM1
          BRANCH    OVRCD,GPMI9000
.
GPMI8000  MOVE      ZERO,EXIT
          GOTO      GPMI9999
.
GPMI9000  MOVE      ONE,EXIT
.
GPMI9999  RETURN
+
. **********************************************************************
. * POST0000 : Set up the postal address: pmscexaf/patmx1af/patma1af   *
. **********************************************************************
POST0000  MATCH     "1",PTCNNEWC
          GOTO      POST5000 IF NOT EQUAL        * using new contacts table ?
.
          PACK      KEY14,PURNO,SP70
          CALL      RSPMCEX1
POST1000  CALL      RKPMCEX1                     * check new contacts table
          BRANCH    OVRCD,POST5000
.
          MATCH     PURNO,PMCEURNO
          GOTO      POST5000 IF NOT EQUAL
.
          MATCH     SP70,PMCETYPE
          GOTO      POST1000 IF EQUAL
.
          MOVE      "tc",KEY2
          PACK      KEY5,KEY2,PMCETYPE
          CALL      RDCODE1
          BRANCH    OVRCD,POST1000
.
          MATCH     "2",TCINDC1
          GOTO      POST1000 IF NOT EQUAL        * postal address ?
.
          MATCH     "1",PMCEACTV
          IF        @EQUAL
            MATCH     PMCEDINA,BKREEDAT          * Check Inactive date
            GOTO      POST1000 IF NOT LESS
          ENDIF
.
          MATCH     SP70,PMCEADD1
          GOTO      POST1000 IF EQUAL            * no postal address
.
.         populate the postal address from pmscexaf
.
          MOVE      PMCEADD1,PTMXADD1
          MOVE      PMCEADD2,PTMXADD2
          MOVE      PMCEADD3,PTMXADD3
          MOVE      PMCEADD4,PTMXADD4
          MOVE      PMCEPOST,PTMXPOST
.
          GOTO      POST9999
.
POST5000  MATCH     SP70,PTMXADD1
          GOTO      POST9999 IF NOT EQUAL        * use patmx1af (default)
.
.         populate the postal address from patma1af
.
          MOVE      PADD1,PTMXADD1
          MOVE      PADD2,PTMXADD2
          MOVE      PSUBRB,PTMXADD3
          MOVE      PADD4,PTMXADD4
          MOVE      PPOST,PTMXPOST

POST9999  RETURN
+
.**********************************************************************
.*                           WOPTN000                                 *
.*                   Display the web option                           *
.**********************************************************************
WOPTN000  MOVE      FALSE,EXIT
          DISPLAY   *P1:10,*EF,*V2LON,"0":
                    *P1:11,"1":
                    *P1:12,"2":
                    *P2:10,*HOFF,". Exit":
                    *P2:11,". Selection Letters":
                    *P2:12,". Print Letters    ";
.
WOPTN100  DISPLAY   *P1:14,*EL,"Select Item : ";
          KEYIN     *P15:14,*V2LON,WOPTION;
          COMPARE   ZERO,WOPTION
          GOTO      WOPTN900 IF EQUAL
          COMPARE   THREE,WOPTION
          GOTO      WOPTN100 IF NOT LESS
.
WOPTN200  DISPLAY   *P1:16,*EL,"Enter Selection ID: ";
          KEYIN     *P21:16,*V2LON,SELECTID;
.
          PACK      SELECTID,SELECTID,SP10
          MATCH     SP4,SELECTID
          GOTO      WOPTN100 IF EQUAL
.
          MOVE      SELECTID,KEY4
          CALL      RDBKLTS1
          IF        OVRCD=0
            COMPARE   ONE,WOPTION              * Selection
            GOTO      WOPTN210 IF EQUAL
            COMPARE   TWO,WOPTION              * Print
            GOTO      WOPTN300 IF EQUAL
          ELSE
            GOTO      WOPTN800 
          ENDIF
.
.         Delete old Booking Letter Selections
.
WOPTN210  PACK      KEY12,SELECTID,SP70
          CALL      RSBKLTL1
          CALL      RKBKLTL1
          BRANCH    OVRCD,WOPTN250
.
          MATCH     SELECTID,BKLLSEID
          GOTO      WOPTN250 IF NOT EQUAL
.
          PACK      KEY12,BKLLSEID,BKLLBOOK,SP70
          CALL      DEBKLTL1
          GOTO      WOPTN210
.
.         Setup Booking Letter Selection Criteria
.
WOPTN250  MOVE      SP1,DEPPAID
          MOVE      FALSE,FLAGDEPN
          MOVE      FALSE,FLAGDEPY
          MATCH     SP1,BKLSDEPS
          IF        !@EQUAL
            MATCH     ANSN,BKLSDEPS 
            IF        @EQUAL
              MOVE      TRUE,FLAGDEPN
              MOVE      BKLSDEPS,DEPPAID
            ENDIF
            MATCH     ANSY,BKLSDEPS 
            IF        @EQUAL
              MOVE      TRUE,FLAGDEPY
              MOVE      BKLSDEPS,DEPPAID
            ENDIF
          ENDIF
.
          MOVE      FALSE,FLAGDATE
          MOVE      SP70,ADMTEST1
          MOVE      SP70,ADMTEST2
          MOVE      ZERO,ADMFORM1
          MOVE      ZERO,ADMFORM2
          MATCH     SP8,BKLSSTDT
          GOTO      WOPTN9999 IF EQUAL
          MATCH     SP8,BKLSENDT
          GOTO      WOPTN9999 IF EQUAL
.
          MOVE      BKLSSTDT,ADMFORM1
          MOVE      BKLSENDT,ADMFORM2
          COMPARE   ADMFORM1,ADMFORM2
          GOTO      WOPTN810 IF LESS
.
          MOVE      TRUE,FLAGDATE
          MOVE      BKLSSTDT,ADMTEST1
          MOVE      BKLSENDT,ADMTEST2
          GOTO      WOPTN999
.
.         Booking Letter Print
.
WOPTN300  PACK      KEY12,SELECTID,SP70
          CALL      RSBKLTL1
          CALL      RKBKLTL1
          BRANCH    OVRCD,WOPTN820
.
          MATCH     SELECTID,BKLLSEID
          GOTO      WOPTN820 IF NOT EQUAL
.
.         position pointer for printing in SET0000
.
          PACK      KEY12,SELECTID,SP70
          CALL      RSBKLTL1
          GOTO      WOPTN999
.
WOPTN800  DISPLAY   *P1:24,*EL,"Selection ID not on file";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      WOPTN200
.
WOPTN810  DISPLAY   *P1:24,*EL,"Invalid Date Range";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      WOPTN200
.
WOPTN820  DISPLAY   *P1:24,*EL,"No Records to print for this Selection ID";
          CALL      HITENTER
          DISPLAY   *P1:24,*EL;
          GOTO      WOPTN200
.
.------ exit option chosen ------
.
WOPTN900  MOVE      TRUE,EXIT
.
WOPTN999  RETURN
+

          INC       STD001IO
          INC       TFILENAM                * Generate Temp File Name
          INC       IBASEQIO/INC            * Sequential Numbers File
          INC       WEBERRIO/INC            * Web Server Error Logging
.
          INC       BOKRC1IO/INC
          INC       BOKLETIO/INC
          INC       BOKLTLIO/INC
          INC       BOKLTSIO/INC
          INC       PATDO1IO/INC
          INC       PATCODIO/INC
          INC       PATMA1IO/INC
          INC       PMSPX2IO/INC
          INC       PATPR1IO/INC
          INC       PMSCEXIO/INC
          INC       WATPROIO/INC
.
          INC       CLPMSPX2
.
